#include <mppi/cost_functions/dubins/distance_map_texture.cuh>
#include <mppi/cost_functions/dubins/first_order_dubins_bicycle_cost.cuh>
#include <mppi/utils/nvtx.cuh>

#include <algorithm>
#include <cfloat>
#include <cmath>

namespace
{
using mppi::cost::detail::distancePointToSegment;

template <class COST_T>
__global__ void generatePreferredLaneCenterDistanceMapKernel(
  const COST_T * cost, const cudaSurfaceObject_t output, const DistanceMapTextureGrid grid)
{
  __shared__ float4 segments[COST_T::kMaxPreferredLaneCenterSegments];
  const auto & data = cost->runtimeData();
  const int count = data.num_preferred_lane_center_segments_;
  const int local = threadIdx.y * blockDim.x + threadIdx.x;
  for (int i = local; i < count; i += blockDim.x * blockDim.y) {
    segments[i] = make_float4(
      data.preferred_lane_center_x0_[i], data.preferred_lane_center_y0_[i],
      data.preferred_lane_center_x1_[i], data.preferred_lane_center_y1_[i]);
  }
  __syncthreads();
  const int gx = blockIdx.x * blockDim.x + threadIdx.x;
  const int gy = blockIdx.y * blockDim.y + threadIdx.y;
  if (gx >= grid.width || gy >= grid.height) return;
  const float x = grid.origin_x + (gx + 0.5F) * grid.resolution;
  const float y = grid.origin_y + (gy + 0.5F) * grid.resolution;
  float distance = kDistanceMapEmptyDistance;
  for (int i = 0; i < count; ++i) {
    const float4 s = segments[i];
    distance = fminf(distance, distancePointToSegment(x, y, s.x, s.y, s.z, s.w));
  }
  surf2Dwrite(distance, output, gx * static_cast<int>(sizeof(float)), gy);
}

template <class COST_T>
__global__ void generateNearestSegmentMapKernel(
  const COST_T * cost, const cudaSurfaceObject_t output, const DistanceMapTextureGrid grid)
{
  static_assert(
    COST_T::kMaxLateralCorridorPoints <= 256,
    "The uint8 nearest-segment texture cannot represent every corridor segment");
  constexpr int kMaxSegments =
    std::max(COST_T::kMaxLateralCorridorPoints, COST_T::kNumTimesteps) - 1;
  __shared__ float4 segments[kMaxSegments];
  __shared__ float inverse_length_squared[kMaxSegments];
  const auto & data = cost->runtimeData();
  const int corridor_points = mppi::memory::loadReadOnly(&data.num_lateral_corridor_points_);
  const bool use_corridor = corridor_points >= 2;
  const int path_points = use_corridor ? corridor_points : COST_T::kNumTimesteps;
  const int segment_count = path_points - 1;
  const int local_thread = static_cast<int>(threadIdx.y * blockDim.x + threadIdx.x);
  const int local_thread_count = static_cast<int>(blockDim.x * blockDim.y);
  for (int segment = local_thread; segment < segment_count; segment += local_thread_count) {
    const float x0 = mppi::memory::loadReadOnly(
      use_corridor ? &data.lateral_corridor_x_[segment] : &data.ref_x_[segment]);
    const float y0 = mppi::memory::loadReadOnly(
      use_corridor ? &data.lateral_corridor_y_[segment] : &data.ref_y_[segment]);
    const float x1 = mppi::memory::loadReadOnly(
      use_corridor ? &data.lateral_corridor_x_[segment + 1] : &data.ref_x_[segment + 1]);
    const float y1 = mppi::memory::loadReadOnly(
      use_corridor ? &data.lateral_corridor_y_[segment + 1] : &data.ref_y_[segment + 1]);
    const float dx = x1 - x0;
    const float dy = y1 - y0;
    const float length_squared = dx * dx + dy * dy;
    segments[segment] = make_float4(x0, y0, dx, dy);
    inverse_length_squared[segment] = length_squared > 1.0E-8F ? 1.0F / length_squared : 0.0F;
  }
  __syncthreads();

  for (int gy = static_cast<int>(blockIdx.y * blockDim.y + threadIdx.y); gy < grid.height;
       gy += static_cast<int>(blockDim.y * gridDim.y)) {
    for (int gx = static_cast<int>(blockIdx.x * blockDim.x + threadIdx.x); gx < grid.width;
         gx += static_cast<int>(blockDim.x * gridDim.x)) {
      const float world_x = grid.origin_x + (static_cast<float>(gx) + 0.5F) * grid.resolution;
      const float world_y = grid.origin_y + (static_cast<float>(gy) + 0.5F) * grid.resolution;
      float best_distance_squared = FLT_MAX;
      NearestSegmentIndex best_segment = 0;
      for (int segment = 0; segment < segment_count; ++segment) {
        const float4 line = segments[segment];
        const float t = fmaxf(
          0.0F, fminf(
                  1.0F, ((world_x - line.x) * line.z + (world_y - line.y) * line.w) *
                          inverse_length_squared[segment]));
        const float error_x = world_x - (line.x + t * line.z);
        const float error_y = world_y - (line.y + t * line.w);
        const float distance_squared = error_x * error_x + error_y * error_y;
        if (distance_squared < best_distance_squared) {
          best_distance_squared = distance_squared;
          best_segment = static_cast<NearestSegmentIndex>(segment);
        }
      }
      surf2Dwrite(best_segment, output, gx * static_cast<int>(sizeof(NearestSegmentIndex)), gy);
    }
  }
}

template <class COST_T>
__global__ void generateStaticDistanceMapKernel(
  const COST_T * cost, const cudaSurfaceObject_t output, const DistanceMapTextureGrid grid,
  const bool update_road_border, const bool update_drivable_area)
{
  constexpr int kMaxRoadSegments = COST_T::kMaxRoadBorderSegments;
  constexpr int kMaxDrivableSegments = COST_T::kMaxDrivableAreaSegments;
  __shared__ float road_x0[kMaxRoadSegments];
  __shared__ float road_y0[kMaxRoadSegments];
  __shared__ float road_x1[kMaxRoadSegments];
  __shared__ float road_y1[kMaxRoadSegments];
  __shared__ float drivable_x0[kMaxDrivableSegments];
  __shared__ float drivable_y0[kMaxDrivableSegments];
  __shared__ float drivable_x1[kMaxDrivableSegments];
  __shared__ float drivable_y1[kMaxDrivableSegments];
  const int local_thread = static_cast<int>(threadIdx.y * blockDim.x + threadIdx.x);
  const int local_thread_count = static_cast<int>(blockDim.x * blockDim.y);
  const auto & data = cost->runtimeData();
  const int road_segment_count = mppi::memory::loadReadOnly(&data.num_road_border_segments_);
  const int drivable_segment_count = mppi::memory::loadReadOnly(&data.num_drivable_area_segments_);
  if (update_road_border) {
    for (int segment = local_thread; segment < road_segment_count; segment += local_thread_count) {
      road_x0[segment] = mppi::memory::loadReadOnly(&data.road_border_x0_[segment]);
      road_y0[segment] = mppi::memory::loadReadOnly(&data.road_border_y0_[segment]);
      road_x1[segment] = mppi::memory::loadReadOnly(&data.road_border_x1_[segment]);
      road_y1[segment] = mppi::memory::loadReadOnly(&data.road_border_y1_[segment]);
    }
  }
  if (update_drivable_area) {
    for (int segment = local_thread; segment < drivable_segment_count;
         segment += local_thread_count) {
      drivable_x0[segment] = mppi::memory::loadReadOnly(&data.drivable_area_x0_[segment]);
      drivable_y0[segment] = mppi::memory::loadReadOnly(&data.drivable_area_y0_[segment]);
      drivable_x1[segment] = mppi::memory::loadReadOnly(&data.drivable_area_x1_[segment]);
      drivable_y1[segment] = mppi::memory::loadReadOnly(&data.drivable_area_y1_[segment]);
    }
  }
  __syncthreads();

  for (int gy = static_cast<int>(blockIdx.y * blockDim.y + threadIdx.y); gy < grid.height;
       gy += static_cast<int>(blockDim.y * gridDim.y)) {
    for (int gx = static_cast<int>(blockIdx.x * blockDim.x + threadIdx.x); gx < grid.width;
         gx += static_cast<int>(blockDim.x * gridDim.x)) {
      const float world_x = grid.origin_x + (static_cast<float>(gx) + 0.5F) * grid.resolution;
      const float world_y = grid.origin_y + (static_cast<float>(gy) + 0.5F) * grid.resolution;
      float2 distances = make_float2(kDistanceMapEmptyDistance, kDistanceMapEmptyDistance);
      if (!update_road_border || !update_drivable_area) {
        distances = surf2Dread<float2>(output, gx * static_cast<int>(sizeof(float2)), gy);
      }

      if (update_road_border) {
        float minimum = kDistanceMapEmptyDistance;
        for (int segment = 0; segment < road_segment_count; ++segment) {
          minimum = fminf(
            minimum, distancePointToSegment(
                       world_x, world_y, road_x0[segment], road_y0[segment], road_x1[segment],
                       road_y1[segment]));
        }
        distances.x = minimum;
      }

      if (update_drivable_area) {
        float minimum = kDistanceMapEmptyDistance;
        for (int segment = 0; segment < drivable_segment_count; ++segment) {
          minimum = fminf(
            minimum, distancePointToSegment(
                       world_x, world_y, drivable_x0[segment], drivable_y0[segment],
                       drivable_x1[segment], drivable_y1[segment]));
        }
        distances.y = minimum;
      }

      surf2Dwrite(distances, output, gx * static_cast<int>(sizeof(float2)), gy);
    }
  }
}

template <class COST_T>
__global__ void generateObstacleDistanceMapKernel(
  const COST_T * cost, const cudaSurfaceObject_t output, const DistanceMapTextureGrid grid)
{
  constexpr int kMaxObstacles = COST_T::kMaxObstacles;
  __shared__ float obstacle_x[kMaxObstacles];
  __shared__ float obstacle_y[kMaxObstacles];
  __shared__ float obstacle_cos[kMaxObstacles];
  __shared__ float obstacle_sin[kMaxObstacles];
  __shared__ float obstacle_half_length[kMaxObstacles];
  __shared__ float obstacle_half_width[kMaxObstacles];
  __shared__ bool obstacle_active[kMaxObstacles];
  const int local_thread = static_cast<int>(threadIdx.y * blockDim.x + threadIdx.x);
  const int local_thread_count = static_cast<int>(blockDim.x * blockDim.y);
  const auto & data = cost->runtimeData();
  const int obstacle_count = mppi::memory::loadReadOnly(&data.num_obstacles_);
  for (int timestep = static_cast<int>(blockIdx.z * blockDim.z + threadIdx.z);
       timestep < grid.time_steps; timestep += static_cast<int>(blockDim.z * gridDim.z)) {
    for (int obstacle = local_thread; obstacle < obstacle_count; obstacle += local_thread_count) {
      obstacle_active[obstacle] = data.obstacleActiveAtStep(obstacle, timestep);
      if (!obstacle_active[obstacle]) continue;
      obstacle_x[obstacle] = mppi::memory::loadReadOnly(&data.obs_x_[obstacle][timestep]);
      obstacle_y[obstacle] = mppi::memory::loadReadOnly(&data.obs_y_[obstacle][timestep]);
      __sincosf(
        mppi::memory::loadReadOnly(&data.obs_yaw_[obstacle][timestep]), &obstacle_sin[obstacle],
        &obstacle_cos[obstacle]);
      obstacle_half_length[obstacle] = mppi::memory::loadReadOnly(&data.obs_half_length_[obstacle]);
      obstacle_half_width[obstacle] = mppi::memory::loadReadOnly(&data.obs_half_width_[obstacle]);
    }
    __syncthreads();
    for (int gy = static_cast<int>(blockIdx.y * blockDim.y + threadIdx.y); gy < grid.height;
         gy += static_cast<int>(blockDim.y * gridDim.y)) {
      for (int gx = static_cast<int>(blockIdx.x * blockDim.x + threadIdx.x); gx < grid.width;
           gx += static_cast<int>(blockDim.x * gridDim.x)) {
        const float world_x = grid.origin_x + (static_cast<float>(gx) + 0.5F) * grid.resolution;
        const float world_y = grid.origin_y + (static_cast<float>(gy) + 0.5F) * grid.resolution;
        float minimum = kDistanceMapEmptyDistance;

        for (int obstacle = 0; obstacle < obstacle_count; ++obstacle) {
          if (!obstacle_active[obstacle]) continue;
          minimum = fminf(
            minimum, signedDistancePointToOrientedBox(
                       world_x, world_y, obstacle_x[obstacle], obstacle_y[obstacle],
                       obstacle_cos[obstacle], obstacle_sin[obstacle],
                       obstacle_half_length[obstacle], obstacle_half_width[obstacle]));
        }

        surf3Dwrite(minimum, output, gx * static_cast<int>(sizeof(float)), gy, timestep);
      }
    }
    __syncthreads();
  }
}
}  // namespace

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setDistanceMapTextureDebugEnabled(const bool enable)
{
  // Store the preference without creating a CUDA/GL context during CPU configuration.
  distance_map_texture_debug_enabled_ = enable;
  const bool visualizer_was_enabled = this->texture_state_.distance_map_visualizer_ != nullptr;
  configureDistanceMapTextureVisualizer(
    this->texture_state_.distance_map_visualizer_, enable && this->GPUMemStatus_,
    this->texture_state_.kStaticDistanceMapWidth, this->texture_state_.kStaticDistanceMapHeight,
    this->texture_state_.kObstacleDistanceMapWidth, this->texture_state_.kObstacleDistanceMapHeight,
    NUM_TIMESTEPS);
  if (
    enable && !visualizer_was_enabled && this->texture_state_.distance_map_visualizer_ != nullptr) {
    this->texture_state_.static_distance_visualization_dirty_ = true;
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::renderDistanceMapTextureDebug()
{
  const bool rendered = renderDistanceMapTextureVisualizer(
    this->texture_state_.distance_map_visualizer_, this->texture_state_.static_distance_texture_,
    this->texture_state_.obstacle_distance_texture_,
    this->texture_state_.road_border_texture_valid_,
    this->texture_state_.drivable_area_texture_valid_, this->texture_state_.obstacle_texture_valid_,
    this->texture_state_.obstacle_texture_has_obstacles_, this->params_.road_border_safe_margin,
    this->params_.drivable_area_safe_margin, this->params_.obstacle_safe_margin,
    this->texture_state_.static_distance_map_grid_.resolution,
    this->texture_state_.obstacle_distance_map_grid_.resolution, this->stream_,
    this->texture_state_.static_distance_visualization_dirty_);
  if (rendered) {
    this->texture_state_.static_distance_visualization_dirty_ = false;
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ bool FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  updateDistanceMapGrid(
    DistanceMapTextureGrid & grid, const int width, const int height, const float resolution)
{
  float minimum_x = this->runtimeData().ref_x_[0];
  float maximum_x = this->runtimeData().ref_x_[0];
  float minimum_y = this->runtimeData().ref_y_[0];
  float maximum_y = this->runtimeData().ref_y_[0];
  for (int timestep = 1; timestep < NUM_TIMESTEPS; ++timestep) {
    minimum_x = std::min(minimum_x, this->runtimeData().ref_x_[timestep]);
    maximum_x = std::max(maximum_x, this->runtimeData().ref_x_[timestep]);
    minimum_y = std::min(minimum_y, this->runtimeData().ref_y_[timestep]);
    maximum_y = std::max(maximum_y, this->runtimeData().ref_y_[timestep]);
  }

  const float center_x = 0.5F * (minimum_x + maximum_x);
  const float center_y = 0.5F * (minimum_y + maximum_y);
  const float snapped_center_x = std::floor(center_x / resolution + 0.5F) * resolution;
  const float snapped_center_y = std::floor(center_y / resolution + 0.5F) * resolution;
  const float next_origin_x = snapped_center_x - 0.5F * static_cast<float>(width) * resolution;
  const float next_origin_y = snapped_center_y - 0.5F * static_cast<float>(height) * resolution;

  const bool changed = grid.width != width || grid.height != height ||
                       grid.time_steps != NUM_TIMESTEPS || grid.origin_x != next_origin_x ||
                       grid.origin_y != next_origin_y || grid.resolution != resolution;
  grid.origin_x = next_origin_x;
  grid.origin_y = next_origin_y;
  grid.resolution = resolution;
  grid.width = width;
  grid.height = height;
  grid.time_steps = NUM_TIMESTEPS;
  return changed;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ bool FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::updateNearestSegmentMapGrid()
{
  const bool use_corridor = runtimeData().num_lateral_corridor_points_ >= 2;
  const float * path_x = use_corridor ? runtimeData().lateral_corridor_x_ : runtimeData().ref_x_;
  const float * path_y = use_corridor ? runtimeData().lateral_corridor_y_ : runtimeData().ref_y_;
  const int path_points = use_corridor ? runtimeData().num_lateral_corridor_points_ : NUM_TIMESTEPS;
  auto & grid = this->texture_state_.nearest_segment_map_grid_;
  if (path_points < 2) {
    grid.width = 0;
    grid.height = 0;
    return false;
  }

  float minimum_x = path_x[0];
  float maximum_x = path_x[0];
  float minimum_y = path_y[0];
  float maximum_y = path_y[0];
  for (int point = 1; point < path_points; ++point) {
    minimum_x = std::min(minimum_x, path_x[point]);
    maximum_x = std::max(maximum_x, path_x[point]);
    minimum_y = std::min(minimum_y, path_y[point]);
    maximum_y = std::max(maximum_y, path_y[point]);
  }

  const float required_width =
    maximum_x - minimum_x + 2.0F * this->texture_state_.kNearestSegmentMapPadding;
  const float required_height =
    maximum_y - minimum_y + 2.0F * this->texture_state_.kNearestSegmentMapPadding;
  const float resolution = std::max(
    this->texture_state_.kNearestSegmentMapMinimumResolution,
    std::max(
      required_width / static_cast<float>(this->texture_state_.kNearestSegmentMapWidth),
      required_height / static_cast<float>(this->texture_state_.kNearestSegmentMapHeight)));
  const float center_x = 0.5F * (minimum_x + maximum_x);
  const float center_y = 0.5F * (minimum_y + maximum_y);
  const float snapped_center_x = std::floor(center_x / resolution + 0.5F) * resolution;
  const float snapped_center_y = std::floor(center_y / resolution + 0.5F) * resolution;
  const float origin_x =
    snapped_center_x -
    0.5F * static_cast<float>(this->texture_state_.kNearestSegmentMapWidth) * resolution;
  const float origin_y =
    snapped_center_y -
    0.5F * static_cast<float>(this->texture_state_.kNearestSegmentMapHeight) * resolution;

  const bool changed = grid.width != this->texture_state_.kNearestSegmentMapWidth ||
                       grid.height != this->texture_state_.kNearestSegmentMapHeight ||
                       grid.time_steps != 1 || grid.origin_x != origin_x ||
                       grid.origin_y != origin_y || grid.resolution != resolution;
  grid.origin_x = origin_x;
  grid.origin_y = origin_y;
  grid.resolution = resolution;
  grid.width = this->texture_state_.kNearestSegmentMapWidth;
  grid.height = this->texture_state_.kNearestSegmentMapHeight;
  grid.time_steps = 1;
  return changed;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::ensureNearestSegmentMapResources()
{
  if (this->texture_state_.nearest_segment_array_ != nullptr) {
    return;
  }

  const cudaChannelFormatDesc channel = cudaCreateChannelDesc<NearestSegmentIndex>();
  HANDLE_ERROR(cudaMallocArray(
    &this->texture_state_.nearest_segment_array_, &channel,
    this->texture_state_.kNearestSegmentMapWidth, this->texture_state_.kNearestSegmentMapHeight,
    cudaArraySurfaceLoadStore));
  cudaResourceDesc resource{};
  resource.resType = cudaResourceTypeArray;
  resource.res.array.array = this->texture_state_.nearest_segment_array_;
  HANDLE_ERROR(cudaCreateSurfaceObject(&this->texture_state_.nearest_segment_surface_, &resource));
  cudaTextureDesc texture{};
  texture.addressMode[0] = cudaAddressModeClamp;
  texture.addressMode[1] = cudaAddressModeClamp;
  texture.filterMode = cudaFilterModePoint;
  texture.readMode = cudaReadModeElementType;
  texture.normalizedCoords = 0;
  HANDLE_ERROR(cudaCreateTextureObject(
    &this->texture_state_.nearest_segment_texture_, &resource, &texture, nullptr));
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::ensurePreferredLaneCenterResources()
{
  auto & state = texture_state_;
  // Check each handle independently so partial allocation can be retried or cleaned up.
  if (!state.preferred_lane_center_array_) {
    const auto channel = cudaCreateChannelDesc<float>();
    HANDLE_ERROR(cudaMallocArray(
      &state.preferred_lane_center_array_, &channel, state.kStaticDistanceMapWidth,
      state.kStaticDistanceMapHeight, cudaArraySurfaceLoadStore));
  }
  cudaResourceDesc resource{};
  resource.resType = cudaResourceTypeArray;
  resource.res.array.array = state.preferred_lane_center_array_;
  if (!state.preferred_lane_center_surface_) {
    HANDLE_ERROR(cudaCreateSurfaceObject(&state.preferred_lane_center_surface_, &resource));
  }
  if (!state.preferred_lane_center_texture_) {
    cudaTextureDesc texture{};
    texture.addressMode[0] = cudaAddressModeClamp;
    texture.addressMode[1] = cudaAddressModeClamp;
    texture.filterMode = cudaFilterModeLinear;
    texture.readMode = cudaReadModeElementType;
    HANDLE_ERROR(
      cudaCreateTextureObject(&state.preferred_lane_center_texture_, &resource, &texture, nullptr));
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::refreshPreferredLaneCenterTexture()
{
  if (!this->GPUMemStatus_) return;
  auto & state = texture_state_;
  if (
    !(this->params_.preferred_lane_center_coeff > 0.0F) ||
    runtimeData().num_preferred_lane_center_segments_ == 0 ||
    !preferred_lane_center_texture_enabled_) {
    const bool publish =
      state.preferred_lane_center_texture_valid_ || preferred_lane_center_geometry_dirty_;
    state.preferred_lane_center_texture_valid_ = false;
    preferred_lane_center_geometry_dirty_ = false;
    if (publish) distanceMapStateToDevice();
    return;
  }
  // Use the same snapped grid definition without changing the environment maps' cache keys.
  const bool moved = updateDistanceMapGrid(
    state.preferred_lane_center_grid_, state.kStaticDistanceMapWidth,
    state.kStaticDistanceMapHeight, state.kStaticDistanceMapResolution);
  if (
    !moved && !preferred_lane_center_geometry_dirty_ && state.preferred_lane_center_texture_valid_)
    return;
  state.preferred_lane_center_texture_valid_ = false;
  mppi::instrumentation::ScopedNvtxRange range(
    "MPPI/distance_map_preferred_lane_center", mppi::instrumentation::NvtxColor::MAP_GENERATION);
  ensurePreferredLaneCenterResources();
  const dim3 block(16, 16);
  const dim3 grid(
    (state.preferred_lane_center_grid_.width + 15) / 16,
    (state.preferred_lane_center_grid_.height + 15) / 16);
  generatePreferredLaneCenterDistanceMapKernel<<<grid, block, 0, this->stream_>>>(
    this->cost_d_, state.preferred_lane_center_surface_, state.preferred_lane_center_grid_);
  HANDLE_ERROR(cudaGetLastError());
  state.preferred_lane_center_texture_valid_ = true;
  ++state.preferred_lane_center_build_count_;
  preferred_lane_center_geometry_dirty_ = false;
  distanceMapStateToDevice();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::ensureDistanceMapResources()
{
  if (this->texture_state_.static_distance_array_ != nullptr) {
    return;
  }

  const cudaChannelFormatDesc static_channel = cudaCreateChannelDesc<float2>();
  HANDLE_ERROR(cudaMallocArray(
    &this->texture_state_.static_distance_array_, &static_channel,
    this->texture_state_.kStaticDistanceMapWidth, this->texture_state_.kStaticDistanceMapHeight,
    cudaArraySurfaceLoadStore));
  const cudaChannelFormatDesc obstacle_channel = cudaCreateChannelDesc<float>();
  const cudaExtent obstacle_extent = make_cudaExtent(
    this->texture_state_.kObstacleDistanceMapWidth, this->texture_state_.kObstacleDistanceMapHeight,
    NUM_TIMESTEPS);
  HANDLE_ERROR(cudaMalloc3DArray(
    &this->texture_state_.obstacle_distance_array_, &obstacle_channel, obstacle_extent,
    cudaArraySurfaceLoadStore));

  cudaResourceDesc static_resource{};
  static_resource.resType = cudaResourceTypeArray;
  static_resource.res.array.array = this->texture_state_.static_distance_array_;
  HANDLE_ERROR(
    cudaCreateSurfaceObject(&this->texture_state_.static_distance_surface_, &static_resource));
  cudaTextureDesc static_texture{};
  static_texture.addressMode[0] = cudaAddressModeClamp;
  static_texture.addressMode[1] = cudaAddressModeClamp;
  static_texture.filterMode = cudaFilterModeLinear;
  static_texture.readMode = cudaReadModeElementType;
  static_texture.normalizedCoords = 0;
  HANDLE_ERROR(cudaCreateTextureObject(
    &this->texture_state_.static_distance_texture_, &static_resource, &static_texture, nullptr));

  cudaResourceDesc obstacle_resource{};
  obstacle_resource.resType = cudaResourceTypeArray;
  obstacle_resource.res.array.array = this->texture_state_.obstacle_distance_array_;
  HANDLE_ERROR(
    cudaCreateSurfaceObject(&this->texture_state_.obstacle_distance_surface_, &obstacle_resource));
  cudaTextureDesc obstacle_texture{};
  obstacle_texture.addressMode[0] = cudaAddressModeClamp;
  obstacle_texture.addressMode[1] = cudaAddressModeClamp;
  obstacle_texture.addressMode[2] = cudaAddressModeClamp;
  obstacle_texture.filterMode = cudaFilterModeLinear;
  obstacle_texture.readMode = cudaReadModeElementType;
  obstacle_texture.normalizedCoords = 0;
  HANDLE_ERROR(cudaCreateTextureObject(
    &this->texture_state_.obstacle_distance_texture_, &obstacle_resource, &obstacle_texture,
    nullptr));
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::rebuildNearestSegmentTexture()
{
  if (!this->GPUMemStatus_) {
    return;
  }
  ensureNearestSegmentMapResources();
  const dim3 block(16, 16, 1);
  const dim3 grid(
    (this->texture_state_.nearest_segment_map_grid_.width + static_cast<int>(block.x) - 1) /
      static_cast<int>(block.x),
    (this->texture_state_.nearest_segment_map_grid_.height + static_cast<int>(block.y) - 1) /
      static_cast<int>(block.y),
    1);
  generateNearestSegmentMapKernel<<<grid, block, 0, this->stream_>>>(
    this->cost_d_, this->texture_state_.nearest_segment_surface_,
    this->texture_state_.nearest_segment_map_grid_);
  HANDLE_ERROR(cudaGetLastError());
  this->texture_state_.nearest_segment_texture_valid_ = true;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::distanceMapStateToDevice()
{
  if (!this->GPUMemStatus_) {
    return;
  }
  HANDLE_ERROR(cudaMemcpyAsync(
    &this->cost_d_->texture_state_, &this->texture_state_, sizeof(DistanceMapTextureState),
    cudaMemcpyHostToDevice, this->stream_));
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  rebuildStaticDistanceTexture(const bool update_road_border, const bool update_drivable_area)
{
  if (!this->GPUMemStatus_ || (!update_road_border && !update_drivable_area)) {
    return;
  }
  mppi::instrumentation::ScopedNvtxRange range(
    "MPPI/distance_map_static", mppi::instrumentation::NvtxColor::MAP_GENERATION);
  ensureDistanceMapResources();
  const dim3 block(16, 16, 1);
  const dim3 grid(
    (this->texture_state_.static_distance_map_grid_.width + static_cast<int>(block.x) - 1) /
      static_cast<int>(block.x),
    (this->texture_state_.static_distance_map_grid_.height + static_cast<int>(block.y) - 1) /
      static_cast<int>(block.y),
    1);
  generateStaticDistanceMapKernel<<<grid, block, 0, this->stream_>>>(
    this->cost_d_, this->texture_state_.static_distance_surface_,
    this->texture_state_.static_distance_map_grid_, update_road_border, update_drivable_area);
  HANDLE_ERROR(cudaGetLastError());
  this->texture_state_.static_distance_visualization_dirty_ = true;
  this->texture_state_.road_border_texture_valid_ =
    this->texture_state_.road_border_texture_valid_ || update_road_border;
  this->texture_state_.drivable_area_texture_valid_ =
    this->texture_state_.drivable_area_texture_valid_ || update_drivable_area;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::rebuildObstacleDistanceTexture()
{
  if (!this->GPUMemStatus_) {
    return;
  }
  this->texture_state_.obstacle_texture_has_obstacles_ = this->runtimeData().num_obstacles_ > 0;
  if (!this->texture_state_.obstacle_texture_has_obstacles_) {
    this->texture_state_.obstacle_texture_valid_ = true;
    return;
  }
  mppi::instrumentation::ScopedNvtxRange range(
    "MPPI/distance_map_obstacle", mppi::instrumentation::NvtxColor::MAP_GENERATION);
  ensureDistanceMapResources();
  const dim3 block(16, 16, 1);
  const dim3 grid(
    (this->texture_state_.obstacle_distance_map_grid_.width + static_cast<int>(block.x) - 1) /
      static_cast<int>(block.x),
    (this->texture_state_.obstacle_distance_map_grid_.height + static_cast<int>(block.y) - 1) /
      static_cast<int>(block.y),
    this->texture_state_.obstacle_distance_map_grid_.time_steps);
  generateObstacleDistanceMapKernel<<<grid, block, 0, this->stream_>>>(
    this->cost_d_, this->texture_state_.obstacle_distance_surface_,
    this->texture_state_.obstacle_distance_map_grid_);
  HANDLE_ERROR(cudaGetLastError());
  this->texture_state_.obstacle_texture_valid_ = true;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  refreshNearestSegmentTexture(const bool geometry_changed)
{
  if (!geometry_changed) {
    return;
  }
  nearest_segment_refresh_pending_ = true;
  nearest_segment_geometry_dirty_ = true;
  if (data_update_active_) {
    return;
  }
  nearest_segment_refresh_pending_ = false;
  nearest_segment_geometry_dirty_ = false;
  refreshNearestSegmentTextureNow();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::refreshNearestSegmentTextureNow()
{
  if (!this->GPUMemStatus_) {
    return;
  }
  const bool lateral_metrics_enabled =
    this->params_.lateral_distance_coeff > 0.0F || this->params_.lateral_yaw_error_coeff > 0.0F ||
    this->params_.remaining_distance_coeff > 0.0F || this->params_.path_overshoot_coeff > 0.0F ||
    this->params_.lateral_boundary_barrier_weight > 0.0F ||
    this->params_.spatial_overspeed_coeff > 0.0F;
  if (!lateral_metrics_enabled) {
    if (this->texture_state_.nearest_segment_texture_valid_) {
      this->texture_state_.nearest_segment_texture_valid_ = false;
      distanceMapStateToDevice();
    }
    return;
  }

  updateNearestSegmentMapGrid();
  if (
    this->texture_state_.nearest_segment_map_grid_.width <= 0 ||
    this->texture_state_.nearest_segment_map_grid_.height <= 0) {
    this->texture_state_.nearest_segment_texture_valid_ = false;
    distanceMapStateToDevice();
    return;
  }
  this->texture_state_.nearest_segment_texture_valid_ = false;
  rebuildNearestSegmentTexture();
  distanceMapStateToDevice();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  refreshDistanceMapTextures(
    const bool obstacle_geometry_changed, const bool road_border_geometry_changed,
    const bool drivable_area_geometry_changed)
{
  distance_map_refresh_pending_ = true;
  obstacle_geometry_dirty_ = obstacle_geometry_dirty_ || obstacle_geometry_changed;
  road_border_geometry_dirty_ = road_border_geometry_dirty_ || road_border_geometry_changed;
  drivable_area_geometry_dirty_ = drivable_area_geometry_dirty_ || drivable_area_geometry_changed;
  if (data_update_active_) {
    return;
  }

  const bool pending_obstacle_geometry_changed = obstacle_geometry_dirty_;
  const bool pending_road_border_geometry_changed = road_border_geometry_dirty_;
  const bool pending_drivable_area_geometry_changed = drivable_area_geometry_dirty_;
  distance_map_refresh_pending_ = false;
  obstacle_geometry_dirty_ = false;
  road_border_geometry_dirty_ = false;
  drivable_area_geometry_dirty_ = false;
  refreshDistanceMapTexturesNow(
    pending_obstacle_geometry_changed, pending_road_border_geometry_changed,
    pending_drivable_area_geometry_changed);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  refreshDistanceMapTexturesNow(
    const bool obstacle_geometry_changed, const bool road_border_geometry_changed,
    const bool drivable_area_geometry_changed)
{
  if (!this->GPUMemStatus_) {
    return;
  }

  refreshPreferredLaneCenterTexture();
  const bool static_grid_changed = updateDistanceMapGrid(
    this->texture_state_.static_distance_map_grid_, this->texture_state_.kStaticDistanceMapWidth,
    this->texture_state_.kStaticDistanceMapHeight,
    this->texture_state_.kStaticDistanceMapResolution);
  const bool obstacle_grid_changed = updateDistanceMapGrid(
    this->texture_state_.obstacle_distance_map_grid_,
    this->texture_state_.kObstacleDistanceMapWidth, this->texture_state_.kObstacleDistanceMapHeight,
    this->texture_state_.kObstacleDistanceMapResolution);

  if (static_grid_changed) {
    this->texture_state_.road_border_texture_valid_ = false;
    this->texture_state_.drivable_area_texture_valid_ = false;
    rebuildStaticDistanceTexture(true, true);
  } else if (road_border_geometry_changed || drivable_area_geometry_changed) {
    rebuildStaticDistanceTexture(road_border_geometry_changed, drivable_area_geometry_changed);
  }

  if (obstacle_grid_changed) {
    this->texture_state_.obstacle_texture_valid_ = false;
  }
  if (obstacle_grid_changed || obstacle_geometry_changed) {
    rebuildObstacleDistanceTexture();
  }
  if (
    static_grid_changed || obstacle_grid_changed || road_border_geometry_changed ||
    drivable_area_geometry_changed || obstacle_geometry_changed) {
    distanceMapStateToDevice();
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::releaseDistanceMapResources()
{
  if (
    this->texture_state_.static_distance_texture_ == 0 &&
    this->texture_state_.obstacle_distance_texture_ == 0 &&
    this->texture_state_.preferred_lane_center_texture_ == 0 &&
    this->texture_state_.nearest_segment_texture_ == 0 &&
    this->texture_state_.static_distance_surface_ == 0 &&
    this->texture_state_.obstacle_distance_surface_ == 0 &&
    this->texture_state_.preferred_lane_center_surface_ == 0 &&
    this->texture_state_.nearest_segment_surface_ == 0 &&
    this->texture_state_.static_distance_array_ == nullptr &&
    this->texture_state_.obstacle_distance_array_ == nullptr &&
    this->texture_state_.preferred_lane_center_array_ == nullptr &&
    this->texture_state_.nearest_segment_array_ == nullptr) {
    return;
  }

  gpuAssert(cudaStreamSynchronize(this->stream_), __FILE__, __LINE__, false);
  if (this->texture_state_.static_distance_surface_ != 0) {
    gpuAssert(
      cudaDestroySurfaceObject(this->texture_state_.static_distance_surface_), __FILE__, __LINE__,
      false);
    this->texture_state_.static_distance_surface_ = 0;
  }
  if (this->texture_state_.obstacle_distance_surface_ != 0) {
    gpuAssert(
      cudaDestroySurfaceObject(this->texture_state_.obstacle_distance_surface_), __FILE__, __LINE__,
      false);
    this->texture_state_.obstacle_distance_surface_ = 0;
  }
  if (this->texture_state_.preferred_lane_center_surface_ != 0) {
    gpuAssert(
      cudaDestroySurfaceObject(this->texture_state_.preferred_lane_center_surface_), __FILE__,
      __LINE__, false);
    this->texture_state_.preferred_lane_center_surface_ = 0;
  }
  if (this->texture_state_.nearest_segment_surface_ != 0) {
    gpuAssert(
      cudaDestroySurfaceObject(this->texture_state_.nearest_segment_surface_), __FILE__, __LINE__,
      false);
    this->texture_state_.nearest_segment_surface_ = 0;
  }
  if (this->texture_state_.static_distance_texture_ != 0) {
    gpuAssert(
      cudaDestroyTextureObject(this->texture_state_.static_distance_texture_), __FILE__, __LINE__,
      false);
    this->texture_state_.static_distance_texture_ = 0;
  }
  if (this->texture_state_.obstacle_distance_texture_ != 0) {
    gpuAssert(
      cudaDestroyTextureObject(this->texture_state_.obstacle_distance_texture_), __FILE__, __LINE__,
      false);
    this->texture_state_.obstacle_distance_texture_ = 0;
  }
  if (this->texture_state_.preferred_lane_center_texture_ != 0) {
    gpuAssert(
      cudaDestroyTextureObject(this->texture_state_.preferred_lane_center_texture_), __FILE__,
      __LINE__, false);
    this->texture_state_.preferred_lane_center_texture_ = 0;
  }
  if (this->texture_state_.nearest_segment_texture_ != 0) {
    gpuAssert(
      cudaDestroyTextureObject(this->texture_state_.nearest_segment_texture_), __FILE__, __LINE__,
      false);
    this->texture_state_.nearest_segment_texture_ = 0;
  }
  if (this->texture_state_.static_distance_array_ != nullptr) {
    gpuAssert(
      cudaFreeArray(this->texture_state_.static_distance_array_), __FILE__, __LINE__, false);
    this->texture_state_.static_distance_array_ = nullptr;
  }
  if (this->texture_state_.obstacle_distance_array_ != nullptr) {
    gpuAssert(
      cudaFreeArray(this->texture_state_.obstacle_distance_array_), __FILE__, __LINE__, false);
    this->texture_state_.obstacle_distance_array_ = nullptr;
  }
  if (this->texture_state_.preferred_lane_center_array_ != nullptr) {
    gpuAssert(
      cudaFreeArray(this->texture_state_.preferred_lane_center_array_), __FILE__, __LINE__, false);
    this->texture_state_.preferred_lane_center_array_ = nullptr;
  }
  if (this->texture_state_.nearest_segment_array_ != nullptr) {
    gpuAssert(
      cudaFreeArray(this->texture_state_.nearest_segment_array_), __FILE__, __LINE__, false);
    this->texture_state_.nearest_segment_array_ = nullptr;
  }
  this->texture_state_.road_border_texture_valid_ = false;
  this->texture_state_.drivable_area_texture_valid_ = false;
  this->texture_state_.obstacle_texture_valid_ = false;
  this->texture_state_.obstacle_texture_has_obstacles_ = false;
  this->texture_state_.preferred_lane_center_texture_valid_ = false;
  this->texture_state_.nearest_segment_texture_valid_ = false;
}
