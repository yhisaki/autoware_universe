#include "mppi/cost_functions/sat.cuh"

#include <mppi/cost_functions/dubins/first_order_dubins_bicycle_cost.cuh>
#include <mppi/cost_functions/path_tracking_geometry.cuh>
#include <mppi/utils/angle_utils.cuh>
#include <mppi/utils/read_only_load.cuh>

#include <mppi/utils/math_utils.h>

#include <algorithm>
#include <array>
#include <cmath>
#include <stdexcept>

namespace
{
using O = FirstOrderDubinsBicycleParams::OutputIndex;
using C = FirstOrderDubinsBicycleParams::ControlIndex;
using mppi::cost::detail::crossTrackDistanceToPolyline;
using mppi::cost::detail::distancePointToSegment;
using mppi::cost::detail::orientedBoxCorners;
using mppi::cost::detail::orientedBoxesOverlap;
using mppi::cost::detail::pathLengthAtProjection;
using mppi::cost::detail::pointInPolygon;
using mppi::cost::detail::projectPointToPolyline;
using mppi::cost::detail::vectorLength;

struct CostPathBuffers
{
  float total_path_length_s = 0.0F;
  int num_corridor = 0;
  bool has_corridor_s = false;
  const float * corridor_x = nullptr;
  const float * corridor_y = nullptr;
  const float * corridor_s = nullptr;
  const float * corridor_ref_velocity = nullptr;
  const float * ref_x = nullptr;
  const float * ref_y = nullptr;
  const float * ref_s = nullptr;
  const float * ref_v = nullptr;
  const float * ref_yaw = nullptr;
};

__host__ __device__ inline int clampTimestep(const int timestep, const int num_timesteps)
{
  if (timestep < 0) {
    return 0;
  }
  if (timestep >= num_timesteps) {
    return num_timesteps - 1;
  }
  return timestep;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ inline CostPathBuffers resolvePathBuffers(
  const FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T> & cost,
  const float * theta_c)
{
  using Cost = FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>;
  CostPathBuffers b;
  // Keep the additional corridor velocity profile in global memory. Staging another
  // kMaxLateralCorridorPoints floats would increase block shared-memory pressure.
  b.corridor_ref_velocity = cost.runtimeData().lateral_corridor_ref_velocity_;
  if (theta_c != nullptr) {
    b.total_path_length_s = theta_c[Cost::kSharedTotalOffset];
    const float n_raw = theta_c[Cost::kSharedNumCorridorOffset];
#ifdef __CUDA_ARCH__
    b.num_corridor = static_cast<int>(fabsf(n_raw) + 0.5F);
#else
    b.num_corridor = static_cast<int>(std::fabs(n_raw) + 0.5F);
#endif
    b.has_corridor_s = (n_raw > 0.0F);
    b.corridor_x = theta_c + Cost::kSharedCorridorXOffset;
    b.corridor_y = theta_c + Cost::kSharedCorridorYOffset;
    b.corridor_s = theta_c + Cost::kSharedCorridorSOffset;
    b.ref_x = theta_c + Cost::kSharedRefXOffset;
    b.ref_y = theta_c + Cost::kSharedRefYOffset;
    b.ref_s = theta_c + Cost::kSharedRefSOffset;
    b.ref_v = theta_c + Cost::kSharedRefVOffset;
    b.ref_yaw = theta_c + Cost::kSharedRefYawOffset;
  } else {
    b.total_path_length_s = cost.runtimeData().lateral_corridor_total_length_s_;
    b.num_corridor = cost.runtimeData().num_lateral_corridor_points_;
    b.has_corridor_s = cost.runtimeData().lateral_corridor_has_s_;
    b.corridor_x = cost.runtimeData().lateral_corridor_x_;
    b.corridor_y = cost.runtimeData().lateral_corridor_y_;
    b.corridor_s = cost.runtimeData().lateral_corridor_s_;
    b.ref_x = cost.runtimeData().ref_x_;
    b.ref_y = cost.runtimeData().ref_y_;
    b.ref_s = cost.runtimeData().ref_s_;
    b.ref_v = cost.runtimeData().ref_v_;
    b.ref_yaw = cost.runtimeData().ref_yaw_;
  }
  return b;
}

template <int NUM_TIMESTEPS>
__host__ __device__ float referenceEndYaw(
  const float * x, const float * y, const float * yaw, int count)
{
  if (count <= 0) {
    return 0.0F;
  }
  if (yaw != nullptr) {
    return yaw[count - 1];
  }
  if (count >= 2) {
#ifdef __CUDA_ARCH__
    return atan2f(y[count - 1] - y[count - 2], x[count - 1] - x[count - 2]);
#else
    return std::atan2(y[count - 1] - y[count - 2], x[count - 1] - x[count - 2]);
#endif
  }
  return 0.0F;
}

template <class PARAMS_T>
__host__ __device__ inline bool needsLateralPathMetrics(const PARAMS_T & params)
{
  return params.lateral_distance_coeff > 0.0F || params.lateral_yaw_error_coeff > 0.0F ||
         params.remaining_distance_coeff > 0.0F || params.path_overshoot_coeff > 0.0F ||
         params.lateral_boundary_barrier_weight > 0.0F || params.spatial_overspeed_coeff > 0.0F;
}

template <class PARAMS_T>
__host__ __device__ inline bool needsTerminalLateralPathMetrics(const PARAMS_T & params)
{
  return params.lateral_distance_coeff > 0.0F || params.lateral_yaw_error_coeff > 0.0F ||
         params.remaining_distance_coeff > 0.0F || params.path_overshoot_coeff > 0.0F ||
         params.lateral_boundary_barrier_weight > 0.0F;
}

__host__ __device__ inline float absLateralDistance(const float signed_lateral_distance)
{
#ifdef __CUDA_ARCH__
  return fabsf(signed_lateral_distance);
#else
  return std::fabs(signed_lateral_distance);
#endif
}

__host__ __device__ inline void markSafetyViolation(
  int * crash_status, const bool violation, const int reason, const int timestep,
  const int geometry_index = -1)
{
  if (crash_status != nullptr && violation) {
    crash_status[0] =
      mppi::safety::merge(crash_status[0], mppi::safety::event(reason, timestep, geometry_index));
  }
}

template <class PARAMS_T>
__host__ __device__ float lateralBoundaryBarrierCost(
  const PARAMS_T & params, const float signed_lateral_distance)
{
  const float clearance_to_boundary =
    params.boundary_threshold - absLateralDistance(signed_lateral_distance);
  return computeSmoothBarrierCost(
    clearance_to_boundary, params.lateral_boundary_soft_margin,
    params.lateral_boundary_barrier_weight);
}

template <class PARAMS_T>
__host__ __device__ void comfortTerms(
  const PARAMS_T & params, const float * y, float & lateral_accel, float & lateral_jerk,
  float & longitudinal_jerk, float & steer_rate)
{
  const float v = y[static_cast<int>(O::BASELINK_VEL_B_X)];
  const float steer = y[static_cast<int>(O::STEER_ANGLE)];
  lateral_accel = v * v * tanf(steer) / fmaxf(params.wheel_base, 1.0E-4F);
  lateral_jerk = y[static_cast<int>(O::LATERAL_JERK)];
  longitudinal_jerk = y[static_cast<int>(O::LONGITUDINAL_JERK)];
  steer_rate = y[static_cast<int>(O::STEERING_RATE)];
}

// Command regularization is separate from physical comfort and jerk-limit evaluation.
// t=0 has no previous command in the horizon; the existing initial-steering penalty
// supplies its measured-angle anchor. Do not treat an unseeded previous command as zero.
template <class PARAMS_T>
__host__ __device__ void commandChangeTerms(
  const PARAMS_T & params, const float * y, int timestep, float & acceleration_command_rate_cost,
  float & steering_command_rate_cost)
{
  acceleration_command_rate_cost = 0.0F;
  steering_command_rate_cost = 0.0F;
  if (timestep <= 0) return;
  const float acceleration_rate = y[static_cast<int>(O::ACCEL_COMMAND_RATE)];
  const float steering_rate = y[static_cast<int>(O::STEER_COMMAND_RATE)];
  acceleration_command_rate_cost =
    params.accel_cmd_rate_coeff * acceleration_rate * acceleration_rate;
  steering_command_rate_cost = params.steer_cmd_rate_coeff * steering_rate * steering_rate;
}

template <int NUM_TIMESTEPS>
__host__ __device__ __noinline__ float distanceToClosestObstacleAnalyticalFallback(
  const float circle_x[kEgoSpineCircleCount], const float circle_y[kEgoSpineCircleCount],
  const float circle_radius, const int t, const FirstOrderDubinsRuntimeData<NUM_TIMESTEPS> & data,
  int * closest_obstacle)
{
  float min_distance = kDistanceMapEmptyDistance;
  if (closest_obstacle != nullptr) *closest_obstacle = -1;
  const int num_obstacles = mppi::memory::loadReadOnly(&data.num_obstacles_);
  for (int i = 0; i < num_obstacles; ++i) {
    if (!data.obstacleActiveAtStep(i, t)) continue;
    float obs_cos;
    float obs_sin;
    const float obs_yaw = mppi::memory::loadReadOnly(&data.obs_yaw_[i][t]);
#ifdef __CUDA_ARCH__
    __sincosf(obs_yaw, &obs_sin, &obs_cos);
#else
    obs_cos = std::cos(obs_yaw);
    obs_sin = std::sin(obs_yaw);
#endif
    const float obs_x = mppi::memory::loadReadOnly(&data.obs_x_[i][t]);
    const float obs_y = mppi::memory::loadReadOnly(&data.obs_y_[i][t]);
    const float obs_half_length = mppi::memory::loadReadOnly(&data.obs_half_length_[i]);
    const float obs_half_width = mppi::memory::loadReadOnly(&data.obs_half_width_[i]);
#pragma unroll
    for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
      const float distance = signedDistancePointToOrientedBox(
                               circle_x[circle], circle_y[circle], obs_x, obs_y, obs_cos, obs_sin,
                               obs_half_length, obs_half_width) -
                             circle_radius;
      if (closest_obstacle != nullptr && distance < min_distance) *closest_obstacle = i;
      min_distance = fminf(min_distance, distance);
    }
  }
  return min_distance;
}

template <int NUM_TIMESTEPS>
__host__ __device__ __noinline__ float cornerBufferCostAnalyticalFallback(
  const float corners_x[4], const float corners_y[4], const float margin,
  const FirstOrderDubinsRuntimeData<NUM_TIMESTEPS> & data)
{
  float total_cost = 0.0F;
  const int segment_count = mppi::memory::loadReadOnly(&data.num_drivable_area_segments_);
  for (int corner = 0; corner < 4; ++corner) {
    float min_distance = kDistanceMapEmptyDistance;
    for (int segment = 0; segment < segment_count; ++segment) {
      const float distance = distancePointToSegment(
        corners_x[corner], corners_y[corner],
        mppi::memory::loadReadOnly(&data.drivable_area_x0_[segment]),
        mppi::memory::loadReadOnly(&data.drivable_area_y0_[segment]),
        mppi::memory::loadReadOnly(&data.drivable_area_x1_[segment]),
        mppi::memory::loadReadOnly(&data.drivable_area_y1_[segment]));

#ifdef __CUDA_ARCH__
      min_distance = fminf(min_distance, distance);
#else
      min_distance = std::min(min_distance, distance);
#endif
    }

    const float violation = fmaxf(0.0F, margin - min_distance);
    total_cost += violation * violation;
  }
  return total_cost;
}
}  // namespace

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  FirstOrderDubinsBicycleCostImpl(cudaStream_t stream)
{
  this->bindToStream(stream);
  this->SHARED_MEM_REQUEST_GRD_BYTES = static_cast<int>(kSharedNumFloats * sizeof(float));
  this->SHARED_MEM_REQUEST_BLK_BYTES = static_cast<int>(kSharedBlkHintFloats * sizeof(float));
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::~FirstOrderDubinsBicycleCostImpl()
{
  freeCudaMem();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::GPUSetup()
{
  if (runtime_data_.size() == 0) {
    runtime_data_.resize(1);
    runtime_data_.data()[0] = runtime_data_device_;
  }
  // Managed copies the embedded device snapshot, including values set before GPU setup.
  runtime_data_device_ = runtime_data_.data()[0];
  PARENT_CLASS::GPUSetup();
  setDistanceMapTextureDebugEnabled(distance_map_texture_debug_enabled_);
  if (!data_update_active_) refreshPreferredLaneCenterTexture();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::freeCudaMem() noexcept
{
  if (this->GPUMemStatus_)
    gpuAssert(cudaStreamSynchronize(this->stream_), __FILE__, __LINE__, false);
  const bool debug_enabled = distance_map_texture_debug_enabled_;
  cleanupNoThrow([&] { setDistanceMapTextureDebugEnabled(false); });
  distance_map_texture_debug_enabled_ = debug_enabled;
  releaseDistanceMapResources();
  PARENT_CLASS::freeCudaMem();
  if (runtime_data_.size() != 0) runtime_data_device_ = runtime_data_.data()[0];
  runtime_data_.resetNoThrow();
  data_update_active_ = false;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ float *
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::projectionHintSlot(
  float * theta_c) const
{
  // Must match mppi::kernels::calcClassSharedMemSize float4 alignment.
  const int grd_floats = mppi::math::int_multiple_const(
                           this->SHARED_MEM_REQUEST_GRD_BYTES, static_cast<int>(sizeof(float4))) /
                         static_cast<int>(sizeof(float));
  const int blk_floats = mppi::math::int_multiple_const(
                           this->SHARED_MEM_REQUEST_BLK_BYTES, static_cast<int>(sizeof(float4))) /
                         static_cast<int>(sizeof(float));
  const int shared_idx = static_cast<int>(blockDim.x * threadIdx.z + threadIdx.x);
  return theta_c + grd_floats + shared_idx * blk_floats;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::initializeCosts(
  float * /*output*/, float * /*control*/, float * theta_c, float /*t_0*/, float /*dt*/) const
{
  const int tid =
    static_cast<int>(threadIdx.x + blockDim.x * (threadIdx.y + blockDim.y * threadIdx.z));
  const int nthreads = static_cast<int>(blockDim.x * blockDim.y * blockDim.z);

  if (tid == 0) {
    const auto & data = runtimeData();
    theta_c[kSharedTotalOffset] =
      mppi::memory::loadReadOnly(&data.lateral_corridor_total_length_s_);
    // Sign encodes has_s: positive = s valid, negative = recompute from xy, 0 = empty.
    theta_c[kSharedNumCorridorOffset] =
      data.lateral_corridor_has_s_
        ? static_cast<float>(mppi::memory::loadReadOnly(&data.num_lateral_corridor_points_))
        : -static_cast<float>(mppi::memory::loadReadOnly(&data.num_lateral_corridor_points_));
  }

  const auto & data = runtimeData();
  for (int i = tid; i < kMaxLateralCorridorPoints; i += nthreads) {
    theta_c[kSharedCorridorXOffset + i] = mppi::memory::loadReadOnly(&data.lateral_corridor_x_[i]);
    theta_c[kSharedCorridorYOffset + i] = mppi::memory::loadReadOnly(&data.lateral_corridor_y_[i]);
    theta_c[kSharedCorridorSOffset + i] = mppi::memory::loadReadOnly(&data.lateral_corridor_s_[i]);
  }
  for (int i = tid; i < NUM_TIMESTEPS; i += nthreads) {
    theta_c[kSharedRefXOffset + i] = mppi::memory::loadReadOnly(&data.ref_x_[i]);
    theta_c[kSharedRefYOffset + i] = mppi::memory::loadReadOnly(&data.ref_y_[i]);
    theta_c[kSharedRefSOffset + i] = mppi::memory::loadReadOnly(&data.ref_s_[i]);
    theta_c[kSharedRefVOffset + i] = mppi::memory::loadReadOnly(&data.ref_v_[i]);
    theta_c[kSharedRefYawOffset + i] = mppi::memory::loadReadOnly(&data.ref_yaw_[i]);
  }

  // One optional first-candidate slot per sample; -1 means no previous projection.
  if (threadIdx.y == 0) {
    *projectionHintSlot(theta_c) = -1.0F;
  }
  __syncthreads();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::paramsToDevice()
{
  PARENT_CLASS::paramsToDevice();
  if (!data_update_active_) refreshPreferredLaneCenterTexture();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::beginDataUpdate()
{
  data_update_active_ = true;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::commitDataUpdate()
{
  if (!data_update_active_) {
    return;
  }
  data_update_active_ = false;

  // Distance-map generation kernels consume the runtime cost data, so preserve this ordering on
  // the cost stream: snapshot upload, map rebuilds, then rollout kernels.
  if (runtime_data_dirty_) {
    uploadDataToDevice();
  }
  if (distance_map_refresh_pending_) {
    const bool obstacle_geometry_changed = obstacle_geometry_dirty_;
    const bool road_border_geometry_changed = road_border_geometry_dirty_;
    const bool drivable_area_geometry_changed = drivable_area_geometry_dirty_;
    distance_map_refresh_pending_ = false;
    obstacle_geometry_dirty_ = false;
    road_border_geometry_dirty_ = false;
    drivable_area_geometry_dirty_ = false;
    refreshDistanceMapTexturesNow(
      obstacle_geometry_changed, road_border_geometry_changed, drivable_area_geometry_changed);
  }
  refreshPreferredLaneCenterTexture();
  if (nearest_segment_refresh_pending_) {
    const bool geometry_changed = nearest_segment_geometry_dirty_;
    nearest_segment_refresh_pending_ = false;
    nearest_segment_geometry_dirty_ = false;
    if (geometry_changed) {
      refreshNearestSegmentTextureNow();
    }
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::dataToDevice()
{
  runtime_data_dirty_ = true;
  if (!data_update_active_) {
    uploadDataToDevice();
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::uploadDataToDevice()
{
  if (!this->GPUMemStatus_) {
    runtime_data_dirty_ = false;
    return;
  }

  if (runtime_data_.size() == 0) {
    runtime_data_.resize(1);
    runtime_data_.data()[0] = runtime_data_device_;
  }
  HANDLE_ERROR(cudaMemcpyAsync(
    &this->cost_d_->runtime_data_device_, this->runtime_data_.data(), sizeof(RuntimeData),
    cudaMemcpyHostToDevice, this->stream_));
  runtime_data_dirty_ = false;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setInitialSteeringAngle(const float steering_angle)
{
  runtimeData().initial_steering_angle_ = std::isfinite(steering_angle) ? steering_angle : 0.0F;
  if (data_update_active_) {
    runtime_data_dirty_ = true;
    return;
  }
  if (this->cost_d_ != nullptr && this->params_.initial_steer_rate_coeff > 0.0F) {
    HANDLE_ERROR(cudaMemcpyAsync(
      &this->cost_d_->runtime_data_device_.initial_steering_angle_,
      &runtimeData().initial_steering_angle_, sizeof(runtimeData().initial_steering_angle_),
      cudaMemcpyHostToDevice, this->stream_));
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setKinematicLimits(const FirstOrderDubinsBicycleKinematicLimitData & limits)
{
  runtimeData().kinematic_limits_ = limits;
  dataToDevice();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setReferenceTrajectory(
    const float * x, const float * y, const float * v, const int count, const float * yaw,
    const float * max_velocity, const std::uint8_t * velocity_limit_active,
    const float * terminal_reference)
{
  const int n = std::max(0, std::min(count, NUM_TIMESTEPS));
  bool projection_geometry_changed = runtimeData().num_lateral_corridor_points_ < 2 &&
                                     !texture_state_.nearest_segment_texture_valid_;
  runtimeData().has_pointwise_velocity_limits_ =
    max_velocity != nullptr && velocity_limit_active != nullptr;
  const float end_yaw = referenceEndYaw<NUM_TIMESTEPS>(x, y, yaw, n);
  for (int i = 0; i < n; ++i) {
    if (runtimeData().num_lateral_corridor_points_ < 2) {
      projection_geometry_changed = projection_geometry_changed ||
                                    runtimeData().ref_x_[i] != x[i] ||
                                    runtimeData().ref_y_[i] != y[i];
    }
    runtimeData().ref_x_[i] = x[i];
    runtimeData().ref_y_[i] = y[i];
    runtimeData().ref_s_[i] =
      i == 0 ? 0.0F : runtimeData().ref_s_[i - 1] + vectorLength(x[i] - x[i - 1], y[i] - y[i - 1]);
    runtimeData().ref_v_[i] = v[i];
    runtimeData().ref_max_velocity_[i] =
      runtimeData().has_pointwise_velocity_limits_ ? max_velocity[i] : 0.0F;
    runtimeData().ref_velocity_limit_active_[i] =
      runtimeData().has_pointwise_velocity_limits_ ? velocity_limit_active[i] : 0U;
    if (yaw != nullptr) {
      runtimeData().ref_yaw_[i] = yaw[i];
    } else if (i >= 1) {
      runtimeData().ref_yaw_[i] = atan2f(y[i] - y[i - 1], x[i] - x[i - 1]);
    } else {  // i == 0
      runtimeData().ref_yaw_[i] = (n >= 2) ? atan2f(y[1] - y[0], x[1] - x[0]) : end_yaw;
    }
  }
  if (n > 0) {
    for (int i = n; i < NUM_TIMESTEPS; ++i) {
      if (runtimeData().num_lateral_corridor_points_ < 2) {
        projection_geometry_changed = projection_geometry_changed ||
                                      runtimeData().ref_x_[i] != x[n - 1] ||
                                      runtimeData().ref_y_[i] != y[n - 1];
      }
      runtimeData().ref_x_[i] = x[n - 1];
      runtimeData().ref_y_[i] = y[n - 1];
      runtimeData().ref_s_[i] = runtimeData().ref_s_[n - 1];
      runtimeData().ref_v_[i] = v[n - 1];
      runtimeData().ref_yaw_[i] = end_yaw;
      runtimeData().ref_max_velocity_[i] = runtimeData().ref_max_velocity_[n - 1];
      runtimeData().ref_velocity_limit_active_[i] = runtimeData().ref_velocity_limit_active_[n - 1];
    }
  } else {
    for (int i = 0; i < NUM_TIMESTEPS; ++i) {
      if (runtimeData().num_lateral_corridor_points_ < 2) {
        projection_geometry_changed = projection_geometry_changed ||
                                      runtimeData().ref_x_[i] != 0.0F ||
                                      runtimeData().ref_y_[i] != 0.0F;
      }
      runtimeData().ref_x_[i] = 0.0F;
      runtimeData().ref_y_[i] = 0.0F;
      runtimeData().ref_s_[i] = 0.0F;
      runtimeData().ref_v_[i] = 0.0F;
      runtimeData().ref_yaw_[i] = 0.0F;
      runtimeData().ref_max_velocity_[i] = 0.0F;
      runtimeData().ref_velocity_limit_active_[i] = 0U;
    }
  }
  if (terminal_reference != nullptr) {
    runtimeData().terminal_reference_[0] = terminal_reference[0];
    runtimeData().terminal_reference_[1] = terminal_reference[1];
    runtimeData().terminal_reference_[2] = terminal_reference[2];
  } else {
    runtimeData().terminal_reference_[0] = runtimeData().ref_x_[NUM_TIMESTEPS - 1];
    runtimeData().terminal_reference_[1] = runtimeData().ref_y_[NUM_TIMESTEPS - 1];
    runtimeData().terminal_reference_[2] = runtimeData().ref_yaw_[NUM_TIMESTEPS - 1];
  }
  dataToDevice();
  refreshNearestSegmentTexture(projection_geometry_changed);
  if (!data_update_active_) refreshPreferredLaneCenterTexture();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setLateralCorridor(
    const float * x, const float * y, const int count, const float * s,
    const float * reference_velocity)
{
  const int n = std::max(0, std::min(count, kMaxLateralCorridorPoints));
  const bool had_corridor = runtimeData().num_lateral_corridor_points_ >= 2;
  bool projection_geometry_changed = !texture_state_.nearest_segment_texture_valid_;
  if (n >= 2) {
    projection_geometry_changed = projection_geometry_changed || !had_corridor ||
                                  runtimeData().num_lateral_corridor_points_ != n;
    for (int i = 0; i < n && !projection_geometry_changed; ++i) {
      projection_geometry_changed = runtimeData().lateral_corridor_x_[i] != x[i] ||
                                    runtimeData().lateral_corridor_y_[i] != y[i];
    }
  } else {
    projection_geometry_changed = projection_geometry_changed || had_corridor;
  }
  runtimeData().num_lateral_corridor_points_ = n;
  runtimeData().lateral_corridor_has_s_ = (s != nullptr && n > 0);
  for (int i = 0; i < n; ++i) {
    runtimeData().lateral_corridor_x_[i] = x[i];
    runtimeData().lateral_corridor_y_[i] = y[i];
    runtimeData().lateral_corridor_s_[i] = runtimeData().lateral_corridor_has_s_ ? s[i] : 0.0F;
    runtimeData().lateral_corridor_ref_velocity_[i] =
      reference_velocity != nullptr ? reference_velocity[i] : 0.0F;
  }
  if (!runtimeData().lateral_corridor_has_s_ && n > 0) {
    runtimeData().lateral_corridor_s_[0] = 0.0F;
    for (int i = 1; i < n; ++i) {
      runtimeData().lateral_corridor_s_[i] =
        runtimeData().lateral_corridor_s_[i - 1] +
        vectorLength(
          runtimeData().lateral_corridor_x_[i] - runtimeData().lateral_corridor_x_[i - 1],
          runtimeData().lateral_corridor_y_[i] - runtimeData().lateral_corridor_y_[i - 1]);
    }
    runtimeData().lateral_corridor_has_s_ = true;
  }
  runtimeData().lateral_corridor_total_length_s_ =
    (n > 0) ? runtimeData().lateral_corridor_s_[n - 1] : 0.0F;
  dataToDevice();
  refreshNearestSegmentTexture(projection_geometry_changed);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::clearLateralCorridor()
{
  const bool projection_geometry_changed = runtimeData().num_lateral_corridor_points_ >= 2 ||
                                           !texture_state_.nearest_segment_texture_valid_;
  runtimeData().num_lateral_corridor_points_ = 0;
  runtimeData().lateral_corridor_has_s_ = false;
  runtimeData().lateral_corridor_total_length_s_ = 0.0F;
  dataToDevice();
  refreshNearestSegmentTexture(projection_geometry_changed);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setOrientedBoxObstacles(
    const float * x, const float * y, const float * yaw, const float * half_length,
    const float * half_width, const int count)
{
  const int n = std::max(0, std::min(count, kMaxObstacles));
  bool geometry_changed = n != runtimeData().num_obstacles_;
  for (int i = 0; i < n && !geometry_changed; ++i) {
    geometry_changed = !runtimeData().obs_is_static_[i] || runtimeData().obs_x_[i][0] != x[i] ||
                       runtimeData().obs_y_[i][0] != y[i] ||
                       runtimeData().obs_yaw_[i][0] != yaw[i] ||
                       runtimeData().obs_half_length_[i] != half_length[i] ||
                       runtimeData().obs_half_width_[i] != half_width[i];
  }
  runtimeData().num_obstacles_ = n;
  for (int i = 0; i < n; ++i) {
    runtimeData().obs_half_length_[i] = half_length[i];
    runtimeData().obs_half_width_[i] = half_width[i];
    runtimeData().obs_is_static_[i] = true;
    for (int t = 0; t < NUM_TIMESTEPS; ++t) {
      runtimeData().obs_x_[i][t] = x[i];
      runtimeData().obs_y_[i][t] = y[i];
      runtimeData().obs_yaw_[i][t] = yaw[i];
    }
  }
  dataToDevice();
  refreshDistanceMapTextures(geometry_changed, false, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setDynamicObstacleHorizon(const float horizon_s, const float dt)
{
  if (!std::isfinite(horizon_s) || horizon_s < 0.0F || !std::isfinite(dt) || dt <= 0.0F) {
    throw std::invalid_argument(
      "Dynamic obstacle horizon must be finite and non-negative; dt must be positive");
  }
  int timesteps = NUM_TIMESTEPS;
  if (horizon_s > 0.0F) {
    timesteps = 0;
    // Obstacles and ego are aligned at x[k+1]. Include the cutoff sample, allowing for
    // float roundoff at exact multiples of dt without rounding up to the next stage.
    while (timesteps < NUM_TIMESTEPS &&
           static_cast<double>(timesteps + 1) * dt <= static_cast<double>(horizon_s) + 1.0E-6) {
      ++timesteps;
    }
  }
  if (runtimeData().dynamic_obstacle_timesteps_ == timesteps) return;
  runtimeData().dynamic_obstacle_timesteps_ = timesteps;
  dataToDevice();
  // The cutoff changes map contents even when obstacle poses have not changed.
  refreshDistanceMapTextures(true, false, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setOrientedBoxObstacleTrajectories(
    const float * x, const float * y, const float * yaw, const float * half_length,
    const float * half_width, const int obstacle_count, const int num_timesteps)
{
  if (obstacle_count > kMaxObstacles) {
    throw std::length_error("MPPI obstacle count exceeds complete GPU/validator coverage");
  }
  const int n = std::max(0, obstacle_count);
  const int nt = std::max(0, std::min(num_timesteps, NUM_TIMESTEPS));
  bool geometry_changed = runtimeData().num_obstacles_ != (nt > 0 ? n : 0);
  runtimeData().num_obstacles_ = nt > 0 ? n : 0;
  constexpr float kStaticPoseTolerance = 1.0E-4F;
  for (int i = 0; i < n; ++i) {
    const bool was_static = runtimeData().obs_is_static_[i];
    bool obstacle_geometry_changed = runtimeData().obs_half_length_[i] != half_length[i] ||
                                     runtimeData().obs_half_width_[i] != half_width[i];
    runtimeData().obs_half_length_[i] = half_length[i];
    runtimeData().obs_half_width_[i] = half_width[i];
    runtimeData().obs_is_static_[i] = true;
    for (int t = 0; t < nt; ++t) {
      const int idx = i * nt + t;
      obstacle_geometry_changed =
        obstacle_geometry_changed || runtimeData().obs_x_[i][t] != x[idx] ||
        runtimeData().obs_y_[i][t] != y[idx] || runtimeData().obs_yaw_[i][t] != yaw[idx];
      runtimeData().obs_x_[i][t] = x[idx];
      runtimeData().obs_y_[i][t] = y[idx];
      runtimeData().obs_yaw_[i][t] = yaw[idx];
      if (
        std::fabs(x[idx] - x[i * nt]) > kStaticPoseTolerance ||
        std::fabs(y[idx] - y[i * nt]) > kStaticPoseTolerance ||
        std::fabs(yaw[idx] - yaw[i * nt]) > kStaticPoseTolerance) {
        runtimeData().obs_is_static_[i] = false;
      }
    }
    obstacle_geometry_changed =
      obstacle_geometry_changed || was_static != runtimeData().obs_is_static_[i];
    if (nt > 0) {
      for (int t = nt; t < NUM_TIMESTEPS; ++t) {
        obstacle_geometry_changed =
          obstacle_geometry_changed ||
          runtimeData().obs_x_[i][t] != runtimeData().obs_x_[i][nt - 1] ||
          runtimeData().obs_y_[i][t] != runtimeData().obs_y_[i][nt - 1] ||
          runtimeData().obs_yaw_[i][t] != runtimeData().obs_yaw_[i][nt - 1];
        runtimeData().obs_x_[i][t] = runtimeData().obs_x_[i][nt - 1];
        runtimeData().obs_y_[i][t] = runtimeData().obs_y_[i][nt - 1];
        runtimeData().obs_yaw_[i][t] = runtimeData().obs_yaw_[i][nt - 1];
      }
    }
    geometry_changed = geometry_changed || obstacle_geometry_changed;
  }
  dataToDevice();
  refreshDistanceMapTextures(geometry_changed, false, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::clearObstacles()
{
  const bool geometry_changed = runtimeData().num_obstacles_ != 0;
  runtimeData().num_obstacles_ = 0;
  dataToDevice();
  refreshDistanceMapTextures(geometry_changed, false, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
std::string FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setPreferredLaneCenterSegments(const std::vector<autoware::mppi_optimizer::Segment> & segments)
{
  std::string status = segments.empty() ? "unavailable" : "active";
  if (segments.size() > kMaxPreferredLaneCenterSegments) status = "overflow";
  if (status == "active") {
    for (const auto & s : segments) {
      if (
        !std::isfinite(s.x0) || !std::isfinite(s.y0) || !std::isfinite(s.x1) ||
        !std::isfinite(s.y1) || (s.x0 == s.x1 && s.y0 == s.y1)) {
        status = "invalid_geometry";
        break;
      }
    }
  }
  auto & data = runtimeData();
  const int count = status == "active" ? static_cast<int>(segments.size()) : 0;
  bool changed = count != data.num_preferred_lane_center_segments_;
  for (int i = 0; i < count; ++i) {
    const auto & s = segments[i];
    changed = changed || data.preferred_lane_center_x0_[i] != s.x0 ||
              data.preferred_lane_center_y0_[i] != s.y0 ||
              data.preferred_lane_center_x1_[i] != s.x1 ||
              data.preferred_lane_center_y1_[i] != s.y1;
    data.preferred_lane_center_x0_[i] = s.x0;
    data.preferred_lane_center_y0_[i] = s.y0;
    data.preferred_lane_center_x1_[i] = s.x1;
    data.preferred_lane_center_y1_[i] = s.y1;
  }
  data.num_preferred_lane_center_segments_ = count;
  preferred_lane_center_geometry_dirty_ |= changed;
  if (changed) texture_state_.preferred_lane_center_texture_valid_ = false;
  if (changed) dataToDevice();
  if (!data_update_active_) refreshPreferredLaneCenterTexture();
  return status;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setPreferredLaneCenterTextureEnabled(const bool enabled)
{
  preferred_lane_center_texture_enabled_ = enabled;
  if (!data_update_active_) refreshPreferredLaneCenterTexture();
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computePreferredLaneCenterCost(const float x, const float y) const
{
  const float weight = this->params_.preferred_lane_center_coeff;
  if (!(weight > 0.0F)) return 0.0F;
  const auto & data = runtimeData();
  const int count = mppi::memory::loadReadOnly(&data.num_preferred_lane_center_segments_);
  if (count == 0) return 0.0F;
#ifdef __CUDA_ARCH__
  if (texture_state_.preferred_lane_center_texture_valid_) {
    const auto & grid = texture_state_.preferred_lane_center_grid_;
    const float tx = (x - grid.origin_x) / grid.resolution;
    const float ty = (y - grid.origin_y) / grid.resolution;
    // Linear interpolation must not use clamped edge texels outside their centers.
    if (tx >= 0.5F && ty >= 0.5F && tx <= grid.width - 0.5F && ty <= grid.height - 0.5F) {
      const float d = tex2D<float>(texture_state_.preferred_lane_center_texture_, tx, ty);
      return weight * d * d;
    }
  }
#endif
  float distance = kDistanceMapEmptyDistance;
  for (int i = 0; i < count; ++i) {
    distance = fminf(
      distance, mppi::cost::detail::distancePointToSegment(
                  x, y, mppi::memory::loadReadOnly(&data.preferred_lane_center_x0_[i]),
                  mppi::memory::loadReadOnly(&data.preferred_lane_center_y0_[i]),
                  mppi::memory::loadReadOnly(&data.preferred_lane_center_x1_[i]),
                  mppi::memory::loadReadOnly(&data.preferred_lane_center_y1_[i])));
  }
  return weight * distance * distance;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setRoadBorderSegments(const std::vector<autoware::mppi_optimizer::Segment> & segments)
{
  if (segments.size() > static_cast<std::size_t>(kMaxRoadBorderSegments)) {
    throw std::length_error("MPPI boundary count exceeds complete GPU/validator coverage");
  }
  const int n = static_cast<int>(segments.size());
  bool geometry_changed = n != runtimeData().num_road_border_segments_;
  for (int i = 0; i < n && !geometry_changed; ++i) {
    geometry_changed = runtimeData().road_border_x0_[i] != segments[i].x0 ||
                       runtimeData().road_border_y0_[i] != segments[i].y0 ||
                       runtimeData().road_border_x1_[i] != segments[i].x1 ||
                       runtimeData().road_border_y1_[i] != segments[i].y1;
  }
  runtimeData().num_road_border_segments_ = n;
  for (int i = 0; i < n; ++i) {
    runtimeData().road_border_x0_[i] = segments[i].x0;
    runtimeData().road_border_y0_[i] = segments[i].y0;
    runtimeData().road_border_x1_[i] = segments[i].x1;
    runtimeData().road_border_y1_[i] = segments[i].y1;
  }
  dataToDevice();
  refreshDistanceMapTextures(false, geometry_changed, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::clearRoadBorders()
{
  const bool geometry_changed = runtimeData().num_road_border_segments_ != 0;
  runtimeData().num_road_border_segments_ = 0;
  dataToDevice();
  refreshDistanceMapTextures(false, geometry_changed, false);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  setDrivableAreaSegments(const std::vector<autoware::mppi_optimizer::Segment> & segments)
{
  if (segments.size() > static_cast<std::size_t>(kMaxDrivableAreaSegments)) {
    throw std::length_error("MPPI boundary count exceeds complete GPU/validator coverage");
  }
  const int n = static_cast<int>(segments.size());
  bool geometry_changed = n != runtimeData().num_drivable_area_segments_;
  for (int i = 0; i < n && !geometry_changed; ++i) {
    geometry_changed = runtimeData().drivable_area_x0_[i] != segments[i].x0 ||
                       runtimeData().drivable_area_y0_[i] != segments[i].y0 ||
                       runtimeData().drivable_area_x1_[i] != segments[i].x1 ||
                       runtimeData().drivable_area_y1_[i] != segments[i].y1;
  }
  runtimeData().num_drivable_area_segments_ = n;
  for (int i = 0; i < n; ++i) {
    runtimeData().drivable_area_x0_[i] = segments[i].x0;
    runtimeData().drivable_area_y0_[i] = segments[i].y0;
    runtimeData().drivable_area_x1_[i] = segments[i].x1;
    runtimeData().drivable_area_y1_[i] = segments[i].y1;
  }
  dataToDevice();
  refreshDistanceMapTextures(false, false, geometry_changed);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
void FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::clearDrivableAreaSegments()
{
  const bool geometry_changed = runtimeData().num_drivable_area_segments_ != 0;
  runtimeData().num_drivable_area_segments_ = 0;
  dataToDevice();
  refreshDistanceMapTextures(false, false, geometry_changed);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::computeTrackValue(
  float x, float y, int timestep, const float * theta_c) const
{
  const auto buf = resolvePathBuffers(*this, theta_c);
  const int t = clampTimestep(timestep, NUM_TIMESTEPS);
  const float dx = x - buf.ref_x[t];
  const float dy = y - buf.ref_y[t];
  return dx * dx + dy * dy;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeHeadingValue(const float yaw, const int timestep, const float * theta_c) const
{
  const auto buf = resolvePathBuffers(*this, theta_c);
  const int t = clampTimestep(timestep, NUM_TIMESTEPS);
  const float yaw_diff = angle_utils::shortestAngularDistance(yaw, buf.ref_yaw[t]);
  return yaw_diff * yaw_diff;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ typename FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::LateralPathMetrics
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeLateralPathMetrics(const float x, const float y, const float yaw, float * theta_c) const
{
  LateralPathMetrics metrics;
  const auto buf = resolvePathBuffers(*this, theta_c);
  const float * poly_x = buf.ref_x;
  const float * poly_y = buf.ref_y;
  const float * poly_s = buf.ref_s;
  const float * poly_ref_velocity = buf.ref_v;
  int n_pts = NUM_TIMESTEPS;
  float total_s = buf.ref_s[NUM_TIMESTEPS - 1];
  bool velocity_profile_is_global = false;
  if (buf.num_corridor >= 2) {
    poly_x = buf.corridor_x;
    poly_y = buf.corridor_y;
    n_pts = buf.num_corridor;
    poly_s = buf.has_corridor_s ? buf.corridor_s : nullptr;
    poly_ref_velocity = buf.corridor_ref_velocity;
    velocity_profile_is_global = true;
    total_s = buf.total_path_length_s;
  }

  int hint_i = -1;
#ifdef __CUDA_ARCH__
  float * hint_slot = nullptr;
  const cudaTextureObject_t nearest_segment_texture =
    texture_state_.nearest_segment_texture_valid_ ? texture_state_.nearest_segment_texture_ : 0;
  if (nearest_segment_texture != 0) {
    const float texture_x = (x - texture_state_.nearest_segment_map_grid_.origin_x) /
                            texture_state_.nearest_segment_map_grid_.resolution;
    const float texture_y = (y - texture_state_.nearest_segment_map_grid_.origin_y) /
                            texture_state_.nearest_segment_map_grid_.resolution;
    if (textureCoordinateInBounds(texture_x, texture_y, texture_state_.nearest_segment_map_grid_)) {
      hint_i =
        static_cast<int>(tex2D<NearestSegmentIndex>(nearest_segment_texture, texture_x, texture_y));
    }
  }
  if (theta_c != nullptr) {
    hint_slot = projectionHintSlot(theta_c);
    if (hint_i < 0) {
      hint_i = static_cast<int>(*hint_slot);
    }
  }
#endif
  // Seeds affect evaluation order only; every query verifies the global nearest segment.
  const auto proj = projectPointToPolyline(x, y, poly_x, poly_y, n_pts, hint_i);
#ifdef __CUDA_ARCH__
  if (hint_slot != nullptr) {
    *hint_slot = static_cast<float>(proj.best_i);
  }
#endif
  metrics.lateral_distance = proj.lateral_distance;
  metrics.best_segment_i = proj.best_i;

  float path_length_s = 0.0F;
  float remaining_distance_s = 0.0F;
  float overshoot_distance_s = 0.0F;
  pathLengthAtProjection(
    proj, poly_x, poly_y, poly_s, n_pts, total_s, path_length_s, remaining_distance_s,
    overshoot_distance_s);
  metrics.path_length_s = path_length_s;
  metrics.spatial_s = path_length_s;
  metrics.remaining_distance_s = remaining_distance_s;
  metrics.overshoot_distance_s = overshoot_distance_s;

  if (n_pts > 1) {
    const int i = proj.best_i;
#ifdef __CUDA_ARCH__
    const float segment_t = fmaxf(0.0F, fminf(1.0F, proj.best_t_raw));
#else
    const float segment_t = std::clamp(proj.best_t_raw, 0.0F, 1.0F);
#endif
    if (poly_s != nullptr) {
      const float s0 = poly_s[i];
      const float s1 = poly_s[i + 1];
      metrics.spatial_s = s0 + segment_t * (s1 - s0);
    }
    if (poly_ref_velocity != nullptr) {
      const float v0 = velocity_profile_is_global
                         ? mppi::memory::loadReadOnly(&poly_ref_velocity[i])
                         : poly_ref_velocity[i];
      const float v1 = velocity_profile_is_global
                         ? mppi::memory::loadReadOnly(&poly_ref_velocity[i + 1])
                         : poly_ref_velocity[i + 1];
      metrics.spatial_ref_velocity = v0 + segment_t * (v1 - v0);
    }
  } else if (n_pts == 1 && poly_ref_velocity != nullptr) {
    metrics.spatial_ref_velocity = velocity_profile_is_global
                                     ? mppi::memory::loadReadOnly(&poly_ref_velocity[0])
                                     : poly_ref_velocity[0];
  }

  float tangent_yaw = 0.0F;
  if (n_pts > 1) {
    const int i = proj.best_i;
    const float dx = poly_x[i + 1] - poly_x[i];
    const float dy = poly_y[i + 1] - poly_y[i];
    const float len_sq = dx * dx + dy * dy;
    if (len_sq > 1.0E-8F) {
#ifdef __CUDA_ARCH__
      tangent_yaw = atan2f(dy, dx);
#else
      tangent_yaw = std::atan2(dy, dx);
#endif
    } else {
      tangent_yaw = buf.ref_yaw[i];
    }
  } else if (buf.num_corridor < 2) {
    tangent_yaw = buf.ref_yaw[0];
  }

  const float yaw_diff = angle_utils::shortestAngularDistance(yaw, tangent_yaw);
  metrics.lateral_yaw_error_sq = yaw_diff * yaw_diff;
  return metrics;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computeLateralDistanceValue(const float x, const float y, float * theta_c) const
{
  return computeLateralPathMetrics(x, y, 0.0F, theta_c).lateral_distance;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ bool FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::exceedsLateralBoundary(const float x, const float y, float * theta_c) const
{
  return absLateralDistance(computeLateralDistanceValue(x, y, theta_c)) >=
         this->params_.boundary_threshold;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ bool
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  egoIntersectsObstacleAtStep(
    const float x, const float y, const float yaw, const int timestep,
    int * intersecting_obstacle) const
{
  if (intersecting_obstacle != nullptr) *intersecting_obstacle = -1;
  int t = timestep;
  if (t < 0) {
    t = 0;
  } else if (t >= NUM_TIMESTEPS) {
    t = NUM_TIMESTEPS - 1;
  }

#ifdef __CUDA_ARCH__
  const float ego_cos = cosf(yaw);
  const float ego_sin = sinf(yaw);
#else
  const float ego_cos = std::cos(yaw);
  const float ego_sin = std::sin(yaw);
#endif
  const float ego_cx = x + this->params_.ego_axle_to_box_center * ego_cos;
  const float ego_cy = y + this->params_.ego_axle_to_box_center * ego_sin;
  const float margin = this->params_.obstacle_collision_margin;
  const float ego_hl = this->params_.ego_length * 0.5F + margin;
  const float ego_hw = this->params_.ego_width * 0.5F + margin;
  const auto & data = runtimeData();
  const int num_obstacles = mppi::memory::loadReadOnly(&data.num_obstacles_);

#ifdef __CUDA_ARCH__
#pragma unroll
#endif
  for (int i = 0; i < num_obstacles; ++i) {
    if (!data.obstacleActiveAtStep(i, t)) continue;
    const float obs_yaw = mppi::memory::loadReadOnly(&data.obs_yaw_[i][t]);
#ifdef __CUDA_ARCH__
    const float obs_cos = cosf(obs_yaw);
    const float obs_sin = sinf(obs_yaw);
#else
    const float obs_cos = std::cos(obs_yaw);
    const float obs_sin = std::sin(obs_yaw);
#endif
    if (orientedBoxesOverlap(
          ego_cx, ego_cy, ego_cos, ego_sin, ego_hl, ego_hw,
          mppi::memory::loadReadOnly(&data.obs_x_[i][t]),
          mppi::memory::loadReadOnly(&data.obs_y_[i][t]), obs_cos, obs_sin,
          mppi::memory::loadReadOnly(&data.obs_half_length_[i]),
          mppi::memory::loadReadOnly(&data.obs_half_width_[i]))) {
      if (intersecting_obstacle != nullptr) *intersecting_obstacle = i;
      return true;
    }
  }
  return false;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  distanceToClosestObstacle(
    const float x, const float y, const float yaw, const int timestep, int * closest_obstacle) const
{
  const int t = timestep < 0 ? 0 : (timestep >= NUM_TIMESTEPS ? NUM_TIMESTEPS - 1 : timestep);
#ifdef __CUDA_ARCH__
  if (
    closest_obstacle == nullptr && texture_state_.obstacle_texture_valid_ &&
    !texture_state_.obstacle_texture_has_obstacles_) {
    return kDistanceMapEmptyDistance;
  }
#endif
  float ego_cos;
  float ego_sin;
#ifdef __CUDA_ARCH__
  __sincosf(yaw, &ego_sin, &ego_cos);
#else
  ego_cos = std::cos(yaw);
  ego_sin = std::sin(yaw);
#endif
  const float ego_cx = x + this->params_.ego_axle_to_box_center * ego_cos;
  const float ego_cy = y + this->params_.ego_axle_to_box_center * ego_sin;
  const float ego_half_length = this->params_.ego_length * 0.5F;
  const float ego_half_width = this->params_.ego_width * 0.5F;
  float circle_x[kEgoSpineCircleCount];
  float circle_y[kEgoSpineCircleCount];
  float circle_radius = 0.0F;
  computeEgoSpineCircles(
    ego_cx, ego_cy, ego_cos, ego_sin, ego_half_length, ego_half_width, circle_x, circle_y,
    circle_radius);
#ifdef __CUDA_ARCH__
  const cudaTextureObject_t obstacle_distance_texture =
    texture_state_.obstacle_texture_valid_ ? texture_state_.obstacle_distance_texture_ : 0;
  if (obstacle_distance_texture != 0 && closest_obstacle == nullptr) {
    float texture_x[kEgoSpineCircleCount];
    float texture_y[kEgoSpineCircleCount];
    bool all_circles_in_bounds = true;
#pragma unroll
    for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
      texture_x[circle] = (circle_x[circle] - texture_state_.obstacle_distance_map_grid_.origin_x) /
                          texture_state_.obstacle_distance_map_grid_.resolution;
      texture_y[circle] = (circle_y[circle] - texture_state_.obstacle_distance_map_grid_.origin_y) /
                          texture_state_.obstacle_distance_map_grid_.resolution;
      all_circles_in_bounds =
        all_circles_in_bounds &&
        textureCoordinateInBounds(
          texture_x[circle], texture_y[circle], texture_state_.obstacle_distance_map_grid_);
    }
    if (all_circles_in_bounds) {
      const float texture_t = static_cast<float>(t) + 0.5F;
      float minimum = kDistanceMapEmptyDistance;
#pragma unroll
      for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
        minimum = fminf(
          minimum,
          tex3D<float>(obstacle_distance_texture, texture_x[circle], texture_y[circle], texture_t) -
            circle_radius);
      }
      return minimum;
    }
  }
#endif
  return distanceToClosestObstacleAnalyticalFallback(
    circle_x, circle_y, circle_radius, t, runtimeData(), closest_obstacle);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeTrackCenterValue(float x, float y, float yaw, int timestep, const float * theta_c) const
{
  const auto buf = resolvePathBuffers(*this, theta_c);
  const int t = clampTimestep(timestep, NUM_TIMESTEPS);
#ifdef __CUDA_ARCH__
  const float x_center = x + this->params_.ego_axle_to_box_center * cosf(yaw);
  const float y_center = y + this->params_.ego_axle_to_box_center * sinf(yaw);
#else
  const float x_center = x + this->params_.ego_axle_to_box_center * std::cos(yaw);
  const float y_center = y + this->params_.ego_axle_to_box_center * std::sin(yaw);
#endif
  const float dx = x_center - buf.ref_x[t];
  const float dy = y_center - buf.ref_y[t];
  return dx * dx + dy * dy;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computeCornerBufferCost(const float x, const float y, const float yaw) const
{
  if (
    mppi::memory::loadReadOnly(&runtimeData().num_drivable_area_segments_) <= 0 ||
    this->params_.corner_buffer_coeff <= 0.0F) {
    return 0.0F;
  }

  const float half_length = this->params_.ego_length * 0.5F;
  const float half_width = this->params_.ego_width * 0.5F;

  float sin_yaw, cos_yaw;
#ifdef __CUDA_ARCH__
  __sincosf(yaw, &sin_yaw, &cos_yaw);
#else
  cos_yaw = std::cos(yaw);
  sin_yaw = std::sin(yaw);
#endif

  const float center_x = x + this->params_.ego_axle_to_box_center * cos_yaw;
  const float center_y = y + this->params_.ego_axle_to_box_center * sin_yaw;

  float corners_x[4];
  float corners_y[4];
  orientedBoxCorners(
    center_x, center_y, cos_yaw, sin_yaw, half_length, half_width, corners_x, corners_y);

  const float margin = this->params_.corner_safe_margin;
  float total_cost = 0.0F;

#ifdef __CUDA_ARCH__
  const cudaTextureObject_t static_distance_texture =
    texture_state_.drivable_area_texture_valid_ ? texture_state_.static_distance_texture_ : 0;
  if (static_distance_texture != 0) {
    bool all_corners_in_bounds = true;
#pragma unroll
    for (int corner = 0; corner < 4; ++corner) {
      const float texture_x =
        (corners_x[corner] - texture_state_.static_distance_map_grid_.origin_x) /
        texture_state_.static_distance_map_grid_.resolution;
      const float texture_y =
        (corners_y[corner] - texture_state_.static_distance_map_grid_.origin_y) /
        texture_state_.static_distance_map_grid_.resolution;
      all_corners_in_bounds =
        all_corners_in_bounds &&
        textureCoordinateInBounds(texture_x, texture_y, texture_state_.static_distance_map_grid_);
    }
    if (all_corners_in_bounds) {
#pragma unroll
      for (int corner = 0; corner < 4; ++corner) {
        const float texture_x =
          (corners_x[corner] - texture_state_.static_distance_map_grid_.origin_x) /
          texture_state_.static_distance_map_grid_.resolution;
        const float texture_y =
          (corners_y[corner] - texture_state_.static_distance_map_grid_.origin_y) /
          texture_state_.static_distance_map_grid_.resolution;
        const float distance = tex2D<float2>(static_distance_texture, texture_x, texture_y).y;
        const float violation = fmaxf(0.0F, margin - distance);
        total_cost += violation * violation;
      }
      return this->params_.corner_buffer_coeff * total_cost;
    }
  }
#endif
  total_cost = cornerBufferCostAnalyticalFallback(corners_x, corners_y, margin, runtimeData());
  return this->params_.corner_buffer_coeff * total_cost;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ bool
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  egoIntersectsRoadBorder(
    const float x, const float y, const float yaw, int * intersecting_segment) const
{
  const float half_length = this->params_.ego_length * 0.5f;
  const float half_width = this->params_.ego_width * 0.5f;
  const float offset = this->params_.ego_axle_to_box_center;
  const float front_ext = offset + half_length;
  const float back_ext = half_length - offset;
  const float left_ext = half_width;
  const float right_ext = half_width;
  const float margin = this->params_.road_border_collision_margin;

  return checkRectSegmentIntersections(
    x, y, yaw, front_ext, back_ext, left_ext, right_ext, margin, runtimeData().road_border_x0_,
    runtimeData().road_border_y0_, runtimeData().road_border_x1_, runtimeData().road_border_y1_,
    mppi::memory::loadReadOnly(&runtimeData().num_road_border_segments_), intersecting_segment);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  distanceToRoadBorder(const float x, const float y, const float yaw, int * closest_segment) const
{
  float cos_yaw;
  float sin_yaw;
#ifdef __CUDA_ARCH__
  __sincosf(yaw, &sin_yaw, &cos_yaw);
#else
  cos_yaw = std::cos(yaw);
  sin_yaw = std::sin(yaw);
#endif
  const float center_x = x + this->params_.ego_axle_to_box_center * cos_yaw;
  const float center_y = y + this->params_.ego_axle_to_box_center * sin_yaw;
  float circle_x[kEgoSpineCircleCount];
  float circle_y[kEgoSpineCircleCount];
  float circle_radius = 0.0F;
  computeEgoSpineCircles(
    center_x, center_y, cos_yaw, sin_yaw, this->params_.ego_length * 0.5F,
    this->params_.ego_width * 0.5F, circle_x, circle_y, circle_radius);
#ifdef __CUDA_ARCH__
  const cudaTextureObject_t static_distance_texture =
    texture_state_.road_border_texture_valid_ ? texture_state_.static_distance_texture_ : 0;
  if (static_distance_texture != 0 && closest_segment == nullptr) {
    float texture_x[kEgoSpineCircleCount];
    float texture_y[kEgoSpineCircleCount];
    bool all_circles_in_bounds = true;
#pragma unroll
    for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
      texture_x[circle] = (circle_x[circle] - texture_state_.static_distance_map_grid_.origin_x) /
                          texture_state_.static_distance_map_grid_.resolution;
      texture_y[circle] = (circle_y[circle] - texture_state_.static_distance_map_grid_.origin_y) /
                          texture_state_.static_distance_map_grid_.resolution;
      all_circles_in_bounds = all_circles_in_bounds && textureCoordinateInBounds(
                                                         texture_x[circle], texture_y[circle],
                                                         texture_state_.static_distance_map_grid_);
    }
    if (all_circles_in_bounds) {
      float minimum = kDistanceMapEmptyDistance;
#pragma unroll
      for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
        minimum = fminf(
          minimum, tex2D<float2>(static_distance_texture, texture_x[circle], texture_y[circle]).x -
                     circle_radius);
      }
      return fmaxf(minimum, 0.0F);
    }
  }
#endif
  return distanceEgoSpineToSegments(
    circle_x, circle_y, circle_radius, runtimeData().road_border_x0_, runtimeData().road_border_y0_,
    runtimeData().road_border_x1_, runtimeData().road_border_y1_,
    mppi::memory::loadReadOnly(&runtimeData().num_road_border_segments_), false, closest_segment);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::distanceToDrivableArea(const float x, const float y, const float yaw) const
{
  float cos_yaw;
  float sin_yaw;
#ifdef __CUDA_ARCH__
  __sincosf(yaw, &sin_yaw, &cos_yaw);
#else
  cos_yaw = std::cos(yaw);
  sin_yaw = std::sin(yaw);
#endif
  const float center_x = x + this->params_.ego_axle_to_box_center * cos_yaw;
  const float center_y = y + this->params_.ego_axle_to_box_center * sin_yaw;
  float circle_x[kEgoSpineCircleCount];
  float circle_y[kEgoSpineCircleCount];
  float circle_radius = 0.0F;
  computeEgoSpineCircles(
    center_x, center_y, cos_yaw, sin_yaw, this->params_.ego_length * 0.5F,
    this->params_.ego_width * 0.5F, circle_x, circle_y, circle_radius);
#ifdef __CUDA_ARCH__
  const cudaTextureObject_t static_distance_texture =
    texture_state_.drivable_area_texture_valid_ ? texture_state_.static_distance_texture_ : 0;
  if (static_distance_texture != 0) {
    float texture_x[kEgoSpineCircleCount];
    float texture_y[kEgoSpineCircleCount];
    bool all_circles_in_bounds = true;
#pragma unroll
    for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
      texture_x[circle] = (circle_x[circle] - texture_state_.static_distance_map_grid_.origin_x) /
                          texture_state_.static_distance_map_grid_.resolution;
      texture_y[circle] = (circle_y[circle] - texture_state_.static_distance_map_grid_.origin_y) /
                          texture_state_.static_distance_map_grid_.resolution;
      all_circles_in_bounds = all_circles_in_bounds && textureCoordinateInBounds(
                                                         texture_x[circle], texture_y[circle],
                                                         texture_state_.static_distance_map_grid_);
    }
    if (all_circles_in_bounds) {
      float minimum = kDistanceMapEmptyDistance;
#pragma unroll
      for (int circle = 0; circle < kEgoSpineCircleCount; ++circle) {
        minimum = fminf(
          minimum, tex2D<float2>(static_distance_texture, texture_x[circle], texture_y[circle]).y -
                     circle_radius);
      }
      return minimum;
    }
  }
#endif
  return distanceEgoSpineToSegments(
    circle_x, circle_y, circle_radius, runtimeData().drivable_area_x0_,
    runtimeData().drivable_area_y0_, runtimeData().drivable_area_x1_,
    runtimeData().drivable_area_y1_,
    mppi::memory::loadReadOnly(&runtimeData().num_drivable_area_segments_), true);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ void
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeGradualCrashCosts(
    const float x, const float y, const float yaw, const int timestep, float & drivable_area_cost,
    float & obstacle_cost, float & road_border_cost, bool * safety_violation,
    int * rollout_status) const
{
  drivable_area_cost =
    this->params_.drivable_area_barrier_weight == 0.0F
      ? 0.0F
      : computeSmoothBarrierCost(
          distanceToDrivableArea(x, y, yaw), this->params_.drivable_area_safe_margin,
          this->params_.drivable_area_barrier_weight);
  obstacle_cost = 0.0F;
  if (this->params_.obstacle_barrier_weight != 0.0F) {
    const float obstacle_distance = distanceToClosestObstacle(x, y, yaw, timestep);
    obstacle_cost = computeSmoothBarrierCost(
      obstacle_distance,
      this->params_.obstacle_collision_margin + this->params_.obstacle_safe_margin,
      this->params_.obstacle_barrier_weight);
    float resolution = 0.0F;
#ifdef __CUDA_ARCH__
    if (texture_state_.obstacle_texture_valid_) {
      resolution = texture_state_.obstacle_distance_map_grid_.resolution;
    }
#endif
    if (
      (safety_violation != nullptr || rollout_status != nullptr) &&
      distanceFieldMayIntersectInflatedRectangle(
        obstacle_distance, this->params_.obstacle_collision_margin, resolution, x, y)) {
      int intersecting_obstacle = -1;
      const bool collision =
        egoIntersectsObstacleAtStep(x, y, yaw, timestep, &intersecting_obstacle);
      if (safety_violation != nullptr) *safety_violation = *safety_violation || collision;
      markSafetyViolation(rollout_status, collision, 2, timestep, intersecting_obstacle);
    }
  }
  road_border_cost = 0.0F;
  if (this->params_.road_border_barrier_weight != 0.0F) {
    const float road_border_distance = distanceToRoadBorder(x, y, yaw);
    road_border_cost = computeSmoothBarrierCost(
      road_border_distance,
      this->params_.road_border_collision_margin + this->params_.road_border_safe_margin,
      this->params_.road_border_barrier_weight);
    float resolution = 0.0F;
#ifdef __CUDA_ARCH__
    if (texture_state_.road_border_texture_valid_) {
      resolution = texture_state_.static_distance_map_grid_.resolution;
    }
#endif
    if (
      (safety_violation != nullptr || rollout_status != nullptr) &&
      distanceFieldMayIntersectInflatedRectangle(
        road_border_distance, this->params_.road_border_collision_margin, resolution, x, y)) {
      int intersecting_segment = -1;
      const bool collision = egoIntersectsRoadBorder(x, y, yaw, &intersecting_segment);
      if (safety_violation != nullptr) *safety_violation = *safety_violation || collision;
      markSafetyViolation(rollout_status, collision, 3, timestep, intersecting_segment);
    }
  }
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
autoware::mppi_optimizer::FirstOrderDubinsMppiCostBreakdown
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeRunningCostBreakdown(
    const Eigen::Ref<const output_array> & y, const Eigen::Ref<const control_array> & u,
    const int timestep, int * crash_status) const
{
  autoware::mppi_optimizer::FirstOrderDubinsMppiCostBreakdown result;
  bool safety_violation = false;

  const float x_pos = y[static_cast<int>(O::BASELINK_POS_I_X)];
  const float y_pos = y[static_cast<int>(O::BASELINK_POS_I_Y)];
  const float yaw = y[static_cast<int>(O::YAW)];
  const float vel = y[static_cast<int>(O::TOTAL_VELOCITY)];

  result.track = this->params_.track_coeff * computeTrackValue(x_pos, y_pos, timestep);
  result.heading = this->params_.heading_coeff * computeHeadingValue(yaw, timestep);
  if (needsLateralPathMetrics(this->params_)) {
    const LateralPathMetrics lateral = computeLateralPathMetrics(x_pos, y_pos, yaw);
    if (
      this->params_.spatial_overspeed_coeff > 0.0F &&
      runtimeData().lateral_corridor_total_length_s_ > 1.0E-6F) {
      const float progress =
        std::clamp(lateral.spatial_s / runtimeData().lateral_corridor_total_length_s_, 0.0F, 1.0F);
      const float overspeed = vel - lateral.spatial_ref_velocity;
      if (overspeed > 0.0F) {
        result.spatial_overspeed =
          this->params_.spatial_overspeed_coeff * progress * overspeed * overspeed;
      }
    }
    result.lateral_distance =
      this->params_.lateral_distance_coeff * lateral.lateral_distance * lateral.lateral_distance;
    result.lateral_boundary = lateralBoundaryBarrierCost(this->params_, lateral.lateral_distance);
    safety_violation = safety_violation || (this->params_.lateral_boundary_barrier_weight > 0.0F &&
                                            absLateralDistance(lateral.lateral_distance) >=
                                              this->params_.boundary_threshold);
    result.lateral_yaw_error = this->params_.lateral_yaw_error_coeff * lateral.lateral_yaw_error_sq;
    result.remaining_distance = this->params_.remaining_distance_coeff *
                                lateral.remaining_distance_s * lateral.remaining_distance_s;
    result.path_overshoot = this->params_.path_overshoot_coeff * lateral.overshoot_distance_s *
                            lateral.overshoot_distance_s;
  }
  result.track_center =
    this->params_.track_center_coeff * computeTrackCenterValue(x_pos, y_pos, yaw, timestep);
  result.preferred_lane_center = computePreferredLaneCenterCost(x_pos, y_pos);
  result.corner_buffer = computeCornerBufferCost(x_pos, y_pos, yaw);
  markSafetyViolation(crash_status, safety_violation, 1, timestep);
  computeGradualCrashCosts(
    x_pos, y_pos, yaw, timestep, result.drivable_area, result.obstacle, result.road_border,
    &safety_violation, crash_status);

  const float accel_cmd = u(static_cast<int>(C::ACCELERATION_CMD));
  const float steer_cmd = u(static_cast<int>(C::STEER_CMD));
  result.acceleration_command = this->params_.accel_cmd_coeff * accel_cmd * accel_cmd;
  result.steering_command = this->params_.steer_cmd_coeff * steer_cmd * steer_cmd;

  float lateral_accel = 0.0F;
  float lateral_jerk = 0.0F;
  float longitudinal_jerk = 0.0F;
  float steer_rate = 0.0F;
  comfortTerms(this->params_, y.data(), lateral_accel, lateral_jerk, longitudinal_jerk, steer_rate);
  result.lateral_acceleration =
    this->params_.lateral_acceleration_coeff * lateral_accel * lateral_accel;
  result.lateral_jerk = this->params_.lateral_jerk_coeff * lateral_jerk * lateral_jerk;
  result.longitudinal_jerk =
    this->params_.longitudinal_jerk_coeff * longitudinal_jerk * longitudinal_jerk;
  result.steering_rate = this->params_.steer_rate_coeff * steer_rate * steer_rate;
  result.initial_steering_rate = computeInitialSteeringRateCost(u.data(), timestep);
  commandChangeTerms(
    this->params_, y.data(), timestep, result.acceleration_command_rate,
    result.steering_command_rate);
  const auto kinematic_cost = computeKinematicLimitCost(
    y[static_cast<int>(O::BASELINK_VEL_B_X)], y[static_cast<int>(O::ACCELERATION)],
    longitudinal_jerk, timestep);
  result.kinematic_velocity_overlimit = kinematic_cost.velocity;
  result.kinematic_acceleration_overlimit = kinematic_cost.acceleration;
  result.kinematic_jerk_overlimit = kinematic_cost.jerk;

  result.running_total = result.componentTotal();
  result.total = result.running_total;
  return result;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
autoware::mppi_optimizer::FirstOrderDubinsMppiCostBreakdown FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computeTerminalCostBreakdown(const Eigen::Ref<const output_array> & y) const
{
  autoware::mppi_optimizer::FirstOrderDubinsMppiCostBreakdown result;
  constexpr int timestep = NUM_TIMESTEPS - 1;
  const float x_pos = y[static_cast<int>(O::BASELINK_POS_I_X)];
  const float y_pos = y[static_cast<int>(O::BASELINK_POS_I_Y)];
  const float yaw = y[static_cast<int>(O::YAW)];

  result.track = this->params_.track_coeff * computeTrackValue(x_pos, y_pos, timestep) *
                 this->params_.track_terminal_scale;
  result.heading = this->params_.heading_coeff * computeHeadingValue(yaw, timestep) *
                   this->params_.track_terminal_scale;
  const float terminal_dx = x_pos - runtimeData().terminal_reference_[0];
  const float terminal_dy = y_pos - runtimeData().terminal_reference_[1];
  const float terminal_yaw_error =
    angle_utils::shortestAngularDistance(yaw, runtimeData().terminal_reference_[2]);
  result.terminal_error =
    this->params_.terminal_error_coeff * (terminal_dx * terminal_dx + terminal_dy * terminal_dy);
  result.terminal_heading =
    this->params_.terminal_heading_coeff * terminal_yaw_error * terminal_yaw_error;
  if (needsTerminalLateralPathMetrics(this->params_)) {
    const LateralPathMetrics lateral = computeLateralPathMetrics(x_pos, y_pos, yaw);
    result.lateral_distance = this->params_.lateral_distance_coeff * lateral.lateral_distance *
                              lateral.lateral_distance * this->params_.track_terminal_scale;
    result.lateral_boundary = lateralBoundaryBarrierCost(this->params_, lateral.lateral_distance);
    result.lateral_yaw_error = this->params_.lateral_yaw_error_coeff *
                               lateral.lateral_yaw_error_sq * this->params_.track_terminal_scale;
    result.remaining_distance = this->params_.remaining_distance_coeff *
                                lateral.remaining_distance_s * lateral.remaining_distance_s *
                                this->params_.track_terminal_scale;
    result.path_overshoot = this->params_.path_overshoot_coeff * lateral.overshoot_distance_s *
                            lateral.overshoot_distance_s * this->params_.track_terminal_scale;
  }
  result.track_center = this->params_.track_center_coeff *
                        computeTrackCenterValue(x_pos, y_pos, yaw, timestep) *
                        this->params_.track_terminal_scale;
  result.preferred_lane_center =
    this->params_.track_terminal_scale * computePreferredLaneCenterCost(x_pos, y_pos);
  result.corner_buffer = computeCornerBufferCost(x_pos, y_pos, yaw);
  computeGradualCrashCosts(
    x_pos, y_pos, yaw, timestep, result.drivable_area, result.obstacle, result.road_border);

  result.terminal_total = result.componentTotal();
  result.total = result.terminal_total;
  return result;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::computeStateCost(
  float * y, int timestep, float * theta_c, int * crash_status) const
{
  const float x_pos = y[static_cast<int>(O::BASELINK_POS_I_X)];
  const float y_pos = y[static_cast<int>(O::BASELINK_POS_I_Y)];
  const float yaw = y[static_cast<int>(O::YAW)];
  const float vel = y[static_cast<int>(O::TOTAL_VELOCITY)];

  const float track_val = computeTrackValue(x_pos, y_pos, timestep, theta_c);
  const float track_cost = this->params_.track_coeff * track_val;
  const float heading_cost =
    this->params_.heading_coeff * computeHeadingValue(yaw, timestep, theta_c);
  float spatial_overspeed_cost = 0.0F;
  float lateral_distance_cost = 0.0F;
  float lateral_boundary_cost = 0.0F;
  float lateral_yaw_error_cost = 0.0F;
  float remaining_distance_cost = 0.0F;
  float path_overshoot_cost = 0.0F;
  bool safety_violation = false;
  if (needsLateralPathMetrics(this->params_)) {
    const LateralPathMetrics lateral = computeLateralPathMetrics(x_pos, y_pos, yaw, theta_c);
    const float corridor_length =
      mppi::memory::loadReadOnly(&runtimeData().lateral_corridor_total_length_s_);
    if (this->params_.spatial_overspeed_coeff > 0.0F && corridor_length > 1.0E-6F) {
      const float progress = fmaxf(0.0F, fminf(1.0F, lateral.spatial_s / corridor_length));
      const float overspeed = vel - lateral.spatial_ref_velocity;
      if (overspeed > 0.0F) {
        spatial_overspeed_cost =
          this->params_.spatial_overspeed_coeff * progress * overspeed * overspeed;
      }
    }
    lateral_distance_cost =
      this->params_.lateral_distance_coeff * lateral.lateral_distance * lateral.lateral_distance;
    lateral_boundary_cost = lateralBoundaryBarrierCost(this->params_, lateral.lateral_distance);
    safety_violation =
      this->params_.lateral_boundary_barrier_weight > 0.0F &&
      absLateralDistance(lateral.lateral_distance) >= this->params_.boundary_threshold;
    lateral_yaw_error_cost = this->params_.lateral_yaw_error_coeff * lateral.lateral_yaw_error_sq;
    remaining_distance_cost = this->params_.remaining_distance_coeff *
                              lateral.remaining_distance_s * lateral.remaining_distance_s;
    path_overshoot_cost = this->params_.path_overshoot_coeff * lateral.overshoot_distance_s *
                          lateral.overshoot_distance_s;
  }
  const float track_center_cost = this->params_.track_center_coeff *
                                  computeTrackCenterValue(x_pos, y_pos, yaw, timestep, theta_c);
  const float corner_buffer_cost = computeCornerBufferCost(x_pos, y_pos, yaw);
  float drivable_area_cost = 0.0F;
  float obstacle_cost = 0.0F;
  float road_border_cost = 0.0F;
  markSafetyViolation(crash_status, safety_violation, 1, timestep);
  computeGradualCrashCosts(
    x_pos, y_pos, yaw, timestep, drivable_area_cost, obstacle_cost, road_border_cost,
    &safety_violation, crash_status);

  return spatial_overspeed_cost + track_cost + heading_cost + lateral_distance_cost +
         lateral_boundary_cost + lateral_yaw_error_cost + remaining_distance_cost +
         path_overshoot_cost + drivable_area_cost + track_center_cost + corner_buffer_cost +
         obstacle_cost + road_border_cost + computePreferredLaneCenterCost(x_pos, y_pos);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
float FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeStateCost(const Eigen::Ref<const output_array> & y, int timestep, int * crash_status)
{
  const float x_pos = y[static_cast<int>(O::BASELINK_POS_I_X)];
  const float y_pos = y[static_cast<int>(O::BASELINK_POS_I_Y)];
  const float yaw = y[static_cast<int>(O::YAW)];
  const float vel = y[static_cast<int>(O::TOTAL_VELOCITY)];

  const float track_val = computeTrackValue(x_pos, y_pos, timestep);
  const float track_cost = this->params_.track_coeff * track_val;
  const float heading_cost = this->params_.heading_coeff * computeHeadingValue(yaw, timestep);
  float spatial_overspeed_cost = 0.0F;
  float lateral_distance_cost = 0.0F;
  float lateral_boundary_cost = 0.0F;
  float lateral_yaw_error_cost = 0.0F;
  float remaining_distance_cost = 0.0F;
  float path_overshoot_cost = 0.0F;
  bool safety_violation = false;
  if (needsLateralPathMetrics(this->params_)) {
    const LateralPathMetrics lateral = computeLateralPathMetrics(x_pos, y_pos, yaw);
    if (
      this->params_.spatial_overspeed_coeff > 0.0F &&
      runtimeData().lateral_corridor_total_length_s_ > 1.0E-6F) {
      const float progress =
        std::clamp(lateral.spatial_s / runtimeData().lateral_corridor_total_length_s_, 0.0F, 1.0F);
      const float overspeed = vel - lateral.spatial_ref_velocity;
      if (overspeed > 0.0F) {
        spatial_overspeed_cost =
          this->params_.spatial_overspeed_coeff * progress * overspeed * overspeed;
      }
    }
    lateral_distance_cost =
      this->params_.lateral_distance_coeff * lateral.lateral_distance * lateral.lateral_distance;
    lateral_boundary_cost = lateralBoundaryBarrierCost(this->params_, lateral.lateral_distance);
    safety_violation =
      this->params_.lateral_boundary_barrier_weight > 0.0F &&
      absLateralDistance(lateral.lateral_distance) >= this->params_.boundary_threshold;
    lateral_yaw_error_cost = this->params_.lateral_yaw_error_coeff * lateral.lateral_yaw_error_sq;
    remaining_distance_cost = this->params_.remaining_distance_coeff *
                              lateral.remaining_distance_s * lateral.remaining_distance_s;
    path_overshoot_cost = this->params_.path_overshoot_coeff * lateral.overshoot_distance_s *
                          lateral.overshoot_distance_s;
  }
  const float track_center_cost =
    this->params_.track_center_coeff * computeTrackCenterValue(x_pos, y_pos, yaw, timestep);
  const float corner_buffer_cost = computeCornerBufferCost(x_pos, y_pos, yaw);
  float drivable_area_cost = 0.0F;
  float obstacle_cost = 0.0F;
  float road_border_cost = 0.0F;
  markSafetyViolation(crash_status, safety_violation, 1, timestep);
  computeGradualCrashCosts(
    x_pos, y_pos, yaw, timestep, drivable_area_cost, obstacle_cost, road_border_cost,
    &safety_violation, crash_status);

  return spatial_overspeed_cost + track_cost + heading_cost + lateral_distance_cost +
         lateral_boundary_cost + lateral_yaw_error_cost + remaining_distance_cost +
         path_overshoot_cost + drivable_area_cost + track_center_cost + corner_buffer_cost +
         obstacle_cost + road_border_cost + computePreferredLaneCenterCost(x_pos, y_pos);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::computeControlCost(
  float * u, int timestep, float * theta_c, int * crash) const
{
  (void)timestep;
  (void)theta_c;
  (void)crash;
  const float accel_cmd = u[static_cast<int>(C::ACCELERATION_CMD)];
  const float steer_cmd = u[static_cast<int>(C::STEER_CMD)];
  return this->params_.accel_cmd_coeff * (accel_cmd * accel_cmd) +
         this->params_.steer_cmd_coeff * (steer_cmd * steer_cmd);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
float FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeControlCost(const Eigen::Ref<const control_array> & u, int timestep, int * crash)
{
  (void)timestep;
  (void)crash;
  const float accel_cmd = u(static_cast<int>(C::ACCELERATION_CMD));
  const float steer_cmd = u(static_cast<int>(C::STEER_CMD));
  return this->params_.accel_cmd_coeff * (accel_cmd * accel_cmd) +
         this->params_.steer_cmd_coeff * (steer_cmd * steer_cmd);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ __noinline__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::terminalCost(
  float * y, float * theta_c) const
{
  if (threadIdx.y == 0) {
    const float x_pos = y[static_cast<int>(O::BASELINK_POS_I_X)];
    const float y_pos = y[static_cast<int>(O::BASELINK_POS_I_Y)];
    const float yaw = y[static_cast<int>(O::YAW)];
    constexpr int timestep = NUM_TIMESTEPS - 1;
    const float track_val = computeTrackValue(x_pos, y_pos, timestep, theta_c);
    const float track_cost =
      this->params_.track_coeff * track_val * this->params_.track_terminal_scale;
    const float heading_cost = this->params_.heading_coeff *
                               computeHeadingValue(yaw, timestep, theta_c) *
                               this->params_.track_terminal_scale;
    const auto & data = runtimeData();
    const float terminal_dx = x_pos - mppi::memory::loadReadOnly(&data.terminal_reference_[0]);
    const float terminal_dy = y_pos - mppi::memory::loadReadOnly(&data.terminal_reference_[1]);
    const float terminal_yaw_error = angle_utils::shortestAngularDistance(
      yaw, mppi::memory::loadReadOnly(&data.terminal_reference_[2]));
    const float terminal_error_cost =
      this->params_.terminal_error_coeff * (terminal_dx * terminal_dx + terminal_dy * terminal_dy);
    const float terminal_heading_cost =
      this->params_.terminal_heading_coeff * terminal_yaw_error * terminal_yaw_error;
    float lateral_distance_cost = 0.0F;
    float lateral_boundary_cost = 0.0F;
    float lateral_yaw_error_cost = 0.0F;
    float remaining_distance_cost = 0.0F;
    float path_overshoot_cost = 0.0F;
    if (needsTerminalLateralPathMetrics(this->params_)) {
      const LateralPathMetrics lateral = computeLateralPathMetrics(x_pos, y_pos, yaw, theta_c);
      lateral_distance_cost = this->params_.lateral_distance_coeff * lateral.lateral_distance *
                              lateral.lateral_distance * this->params_.track_terminal_scale;
      lateral_boundary_cost = lateralBoundaryBarrierCost(this->params_, lateral.lateral_distance);
      lateral_yaw_error_cost = this->params_.lateral_yaw_error_coeff *
                               lateral.lateral_yaw_error_sq * this->params_.track_terminal_scale;
      remaining_distance_cost = this->params_.remaining_distance_coeff *
                                lateral.remaining_distance_s * lateral.remaining_distance_s *
                                this->params_.track_terminal_scale;
      path_overshoot_cost = this->params_.path_overshoot_coeff * lateral.overshoot_distance_s *
                            lateral.overshoot_distance_s * this->params_.track_terminal_scale;
    }
    const float track_center_cost = this->params_.track_center_coeff *
                                    computeTrackCenterValue(x_pos, y_pos, yaw, timestep, theta_c) *
                                    this->params_.track_terminal_scale;
    const float corner_buffer_cost = computeCornerBufferCost(x_pos, y_pos, yaw);
    float drivable_area_cost = 0.0F;
    float obstacle_cost = 0.0F;
    float road_border_cost = 0.0F;
    computeGradualCrashCosts(
      x_pos, y_pos, yaw, NUM_TIMESTEPS - 1, drivable_area_cost, obstacle_cost, road_border_cost);
    return track_cost + heading_cost + terminal_error_cost + terminal_heading_cost +
           lateral_distance_cost + lateral_boundary_cost + lateral_yaw_error_cost +
           remaining_distance_cost + path_overshoot_cost + drivable_area_cost + track_center_cost +
           corner_buffer_cost + obstacle_cost + road_border_cost +
           this->params_.track_terminal_scale * computePreferredLaneCenterCost(x_pos, y_pos);
  }
  return 0.0F;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ FirstOrderDubinsBicycleKinematicCost
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeKinematicLimitCost(
    const float velocity, const float longitudinal_acceleration, const float longitudinal_jerk,
    const int timestep) const
{
  const auto & stored_limits = runtimeData().kinematic_limits_;
  FirstOrderDubinsBicycleKinematicLimitData limits;
  limits.active_mask = mppi::memory::loadReadOnly(&stored_limits.active_mask);
  limits.min_velocity = mppi::memory::loadReadOnly(&stored_limits.min_velocity);
  limits.max_velocity = mppi::memory::loadReadOnly(&stored_limits.max_velocity);
  limits.min_longitudinal_acceleration =
    mppi::memory::loadReadOnly(&stored_limits.min_longitudinal_acceleration);
  limits.max_longitudinal_acceleration =
    mppi::memory::loadReadOnly(&stored_limits.max_longitudinal_acceleration);
  limits.min_longitudinal_jerk = mppi::memory::loadReadOnly(&stored_limits.min_longitudinal_jerk);
  limits.max_longitudinal_jerk = mppi::memory::loadReadOnly(&stored_limits.max_longitudinal_jerk);
  const int bounded_timestep =
    timestep < 0 ? 0 : (timestep >= NUM_TIMESTEPS ? NUM_TIMESTEPS - 1 : timestep);
  if (
    runtimeData().has_pointwise_velocity_limits_ &&
    mppi::memory::loadReadOnly(&runtimeData().ref_velocity_limit_active_[bounded_timestep]) != 0U) {
    limits.active_mask |= kVelocityLimitActive;
    limits.min_velocity = 0.0F;
    limits.max_velocity =
      mppi::memory::loadReadOnly(&runtimeData().ref_max_velocity_[bounded_timestep]);
  }
  return computeCappedKinematicIntervalCost(
    limits, this->params_.overlimit_coeff, this->params_.crash_contact_penalty, velocity,
    longitudinal_acceleration, longitudinal_jerk);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computeInitialSteeringRateCost(const float * control, const int timestep) const
{
  if (timestep != 0 || this->params_.initial_steer_rate_coeff <= 0.0F) {
    return 0.0F;
  }
  const float control_dt = fmaxf(DYN_PARAMS_T::kControlDt, 1.0E-6F);
  const float steer_cmd = control[static_cast<int>(C::STEER_CMD)];
  const float initial_steer_rate =
    (steer_cmd - mppi::memory::loadReadOnly(&runtimeData().initial_steering_angle_)) / control_dt;
  return this->params_.initial_steer_rate_coeff * initial_steer_rate * initial_steer_rate *
         static_cast<float>(NUM_TIMESTEPS);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
float FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeComfortCost(
    const Eigen::Ref<const control_array> & /*u*/, const Eigen::Ref<const output_array> & y,
    int timestep)
{
  float lateral_accel = 0.0F;
  float lateral_jerk = 0.0F;
  float longitudinal_jerk = 0.0F;
  float steer_rate = 0.0F;
  comfortTerms(this->params_, y.data(), lateral_accel, lateral_jerk, longitudinal_jerk, steer_rate);
  const auto kinematic_cost = computeKinematicLimitCost(
    y(static_cast<int>(O::BASELINK_VEL_B_X)), y(static_cast<int>(O::ACCELERATION)),
    longitudinal_jerk, timestep);
  return this->params_.lateral_acceleration_coeff * lateral_accel * lateral_accel +
         this->params_.lateral_jerk_coeff * lateral_jerk * lateral_jerk +
         this->params_.longitudinal_jerk_coeff * longitudinal_jerk * longitudinal_jerk +
         this->params_.steer_rate_coeff * steer_rate * steer_rate + kinematic_cost.total;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::computeComfortCost(
  float * /*u*/, float * y, int timestep) const
{
  float lateral_accel = 0.0F;
  float lateral_jerk = 0.0F;
  float longitudinal_jerk = 0.0F;
  float steer_rate = 0.0F;
  comfortTerms(this->params_, y, lateral_accel, lateral_jerk, longitudinal_jerk, steer_rate);
  const auto kinematic_cost = computeKinematicLimitCost(
    y[static_cast<int>(O::BASELINK_VEL_B_X)], y[static_cast<int>(O::ACCELERATION)],
    longitudinal_jerk, timestep);
  return this->params_.lateral_acceleration_coeff * lateral_accel * lateral_accel +
         this->params_.lateral_jerk_coeff * lateral_jerk * lateral_jerk +
         this->params_.longitudinal_jerk_coeff * longitudinal_jerk * longitudinal_jerk +
         this->params_.steer_rate_coeff * steer_rate * steer_rate + kinematic_cost.total;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__host__ __device__ float FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T,
  DYN_PARAMS_T>::computeCommandChangeCost(const float * u, const float * y, int timestep) const
{
  float acceleration_command_rate_cost, steering_command_rate_cost;
  commandChangeTerms(
    this->params_, y, timestep, acceleration_command_rate_cost, steering_command_rate_cost);
  return computeInitialSteeringRateCost(u, timestep) + acceleration_command_rate_cost +
         steering_command_rate_cost;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
float FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::
  computeRunningCost(
    const Eigen::Ref<const output_array> & y, const Eigen::Ref<const control_array> & u,
    int timestep, int * crash)
{
  const float state_cost = computeStateCost(y, timestep, crash);
  return state_cost + computeControlCost(u, timestep, crash) + computeComfortCost(u, y, timestep) +
         computeCommandChangeCost(u.data(), y.data(), timestep);
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
__device__ float
FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::computeRunningCost(
  float * y, float * u, int timestep, float * theta_c, int * crash) const
{
  if (threadIdx.y == 0) {
    const float state_cost = computeStateCost(y, timestep, theta_c, crash);
    return state_cost + computeControlCost(u, timestep, theta_c, crash) +
           computeComfortCost(u, y, timestep) + computeCommandChangeCost(u, y, timestep);
  }
  return 0.0F;
}

template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
constexpr int
  FirstOrderDubinsBicycleCostImpl<CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::kMaxObstacles;
template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
constexpr int FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::kMaxDrivablePolygonVertices;
template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
constexpr int FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::kMaxRoadBorderSegments;
template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
constexpr int FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::kMaxDrivableAreaSegments;
template <class CLASS_T, int NUM_TIMESTEPS, class PARAMS_T, class DYN_PARAMS_T>
constexpr int FirstOrderDubinsBicycleCostImpl<
  CLASS_T, NUM_TIMESTEPS, PARAMS_T, DYN_PARAMS_T>::kMaxLateralCorridorPoints;
